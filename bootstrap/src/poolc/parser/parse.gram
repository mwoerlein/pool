//DECL
%baseclass-preinclude poolc/parser/Symbol.hpp
%union
{
    long u_number;
    String *u_string;
    
    TranslationUnitNode *u_unit;
    NamespaceDeclNode *u_namespace_decl;
    UseStatementNode *u_use_stmt;
    
    ClassDeclNode *u_class_decl;
    MethodDeclNode *u_method_decl;
    VariableDeclNode *u_variable_decl;
    MutableCollection<VariableDeclNode> *u_variable_decl_list;
    
    InstructionNode *u_instruction;
    MutableCollection<InstructionNode> *u_instruction_list;
    IntConstAssignNode *u_const_int_decl;
    CStringConstAssignNode *u_const_cstring_decl;
    
    TypeRefNode *u_type;
    MutableCollection<TypeRefNode> *u_type_list;
}
%lsp-needed

%token COLON_EQUAL
%right EQUAL
%left DOT

%token ABSTRACT
%token AS
%token CLASS
%token EXTENDS
%token GLOBAL
%token NAMESPACE
%token RETURN
%token THIS
%token USE

%token CSTRING
%token INT

%token NAKED
%token PASM

%token <u_string> STRING
%token <u_string> ID
%token <u_string> FQN
%token <u_number> NUMBER

%type <u_unit> translation_unit
%type <u_namespace_decl> namespace_decl
%type <u_use_stmt> use_stmt

%type <u_class_decl> class_decl
%type <u_class_decl> class_body_empty
%type <u_method_decl> method_decl
%type <u_variable_decl> variable_decl
%type <u_variable_decl_list> variable_decl_list
%type <u_variable_decl_list> variable_decl_list_empty

%type <u_instruction> instruction
%type <u_instruction_list> instruction_list_empty
%type <u_instruction> inline_pasm_inst
%type <u_const_int_decl> const_int_decl
%type <u_const_cstring_decl> const_cstring_decl

%type <u_type> type
%type <u_type_list> type_list
%type <u_type_list> type_list_empty


//=
%%
//RULES
translation_unit:
    namespace_decl
    {
        $$ = driver.getUnit();
        $$->ns = $1;
    }
|   translation_unit use_stmt
    {
        $$ = $1;
        if ($2) { $$->uses.add(*$2); }
    }
|   translation_unit class_decl
    {
        $$ = $1;
        if ($2) { $$->classes.add(*$2); }
    }
;

namespace_decl:
    NAMESPACE ID ';'
    {
        $$ = &driver.env().create<NamespaceDeclNode>();
        $$->name = *$2;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @3.last_line;
        $$->last_column = @3.last_column;
        
        $2->destroy();
    }
|   NAMESPACE FQN ';'
    {
        $$ = &driver.env().create<NamespaceDeclNode>();
        $$->name = *$2;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @3.last_line;
        $$->last_column = @3.last_column;
        
        $2->destroy();
    }
;

use_stmt:
    USE FQN ';'
    {
        $$ = &driver.env().create<UseStatementNode>();
        $$->name = *$2;
        $$->alias = $$->name.lastPart();
        $2->destroy();
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @3.last_line;
        $$->last_column = @3.last_column;
    }
|   USE FQN AS ID ';'
    {
        $$ = &driver.env().create<UseStatementNode>();
        $$->name = *$2;
        $$->alias = *$4;
        $2->destroy();
        $4->destroy();
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @5.last_line;
        $$->last_column = @5.last_column;
    }
;

class_decl:
    CLASS ID '{' class_body_empty '}'
    {
        $$ = $4;
        $$->name = *$2;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @5.last_line;
        $$->last_column = @5.last_column;
        
        $2->destroy();
    }
|   CLASS ID EXTENDS type_list '{' class_body_empty '}'
    {
        $$ = $6;
        $$->name = *$2;
        $$->extends.addAll(*$4);
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @7.last_line;
        $$->last_column = @7.last_column;
        
        $2->destroy();
        $4->destroy();
    }
;

class_body_empty:
    // empty
    {
        $$ = &driver.env().create<ClassDeclNode>();
    }
|   class_body_empty method_decl
    {
        $$ = $1;
        if ($2) { $1->methods.add(*$2); }
    }
|   class_body_empty variable_decl ';'
    {
        $$ = $1;
        if ($2) { $$->variables.add(*$2); }
    }
|   class_body_empty const_int_decl ';'
    {
        $$ = $1;
        if ($2) { $$->intConsts.add(*$2); }
    }
|   class_body_empty const_cstring_decl ';'
    {
        $$ = $1;
        if ($2) { $$->consts.add(*$2); }
    }
|   class_body_empty error ';'
;

type:
    ID
    {
        ClassRefNode & ref = driver.env().create<ClassRefNode>();
        ref.name = *$1;
        $$ = &ref;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @1.last_line;
        $$->last_column = @1.last_column;
        
        $1->destroy();
    }
|   FQN
    {
        ClassRefNode & ref = driver.env().create<ClassRefNode>();
        ref.name = *$1;
        $$ = &ref;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @1.last_line;
        $$->last_column = @1.last_column;
        
        $1->destroy();
    }
|   INT
    {
        $$ = &driver.env().create<IntRefNode>();
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @1.last_line;
        $$->last_column = @1.last_column;
    }
|   CSTRING
    {
        $$ = &driver.env().create<CStringRefNode>();
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @1.last_line;
        $$->last_column = @1.last_column;
    }
;

type_list:
    type
    {
        $$ = &driver.env().create<LinkedList<TypeRefNode>>();
        $$->add(*$1);
    }
|   type_list ',' type
    {
        $$ = $1;
        $$->add(*$3);
    }
;

type_list_empty:
    //empty
    {
        $$ = &driver.env().create<LinkedList<TypeRefNode>>();
    }
|   type_list
;

variable_decl:
    type ID
    {
        $$ = &driver.env().create<VariableDeclNode>();
        $$->name = *$2;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @2.last_line;
        $$->last_column = @2.last_column;
        
        $1->destroy();
        $2->destroy();
    }
;

variable_decl_list:
    variable_decl
    {
        $$ = &driver.env().create<LinkedList<VariableDeclNode>>();
        $$->add(*$1);
    }
|   variable_decl_list ',' variable_decl
    {
        $$ = $1;
        $$->add(*$3);
    }
;

variable_decl_list_empty:
    //empty
    {
        $$ = &driver.env().create<LinkedList<VariableDeclNode>>();
    }
|   variable_decl_list
;

const_int_decl:
    variable_decl COLON_EQUAL NUMBER
    {
        $$ = &driver.env().create<IntConstAssignNode>();
        $$->name = $1->name;
        $$->value = $3;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @3.last_line;
        $$->last_column = @3.last_column;
        
        $1->destroy();
    }
|   GLOBAL variable_decl COLON_EQUAL NUMBER
    {
        $$ = &driver.env().create<IntConstAssignNode>();
        $$->name = $2->name;
        $$->value = $4;
        $$->global = true;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @4.last_line;
        $$->last_column = @4.last_column;
        
        $2->destroy();
    }
;

const_cstring_decl:
    variable_decl COLON_EQUAL STRING
    {
        $$ = &driver.env().create<CStringConstAssignNode>();
        $$->name = $1->name;
        $$->value = *$3;
        $1->destroy();
        $3->destroy();
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @3.last_line;
        $$->last_column = @3.last_column;
    }
;

method_decl:
    '<' type_list_empty '>' ID '(' variable_decl_list_empty ')' '{' instruction_list_empty '}'
    {
        $$ = &driver.env().create<MethodDeclNode>();
        $$->name = *$4;
        $$->body.addAll(*$9);
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @10.last_line;
        $$->last_column = @10.last_column;
        
        $2->destroy();
        $4->destroy();
        $6->destroy();
        $9->destroy();
    }
|   GLOBAL '<' type_list_empty '>' ID '(' variable_decl_list_empty ')' '{' instruction_list_empty '}'
    {
        $$ = &driver.env().create<MethodDeclNode>();
        $$->name = *$5;
        $$->body.addAll(*$10);
        $$->scope = scope_class;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @11.last_line;
        $$->last_column = @11.last_column;
        
        $3->destroy();
        $5->destroy();
        $7->destroy();
        $10->destroy();
    }
|   NAKED '<' type_list_empty '>' ID '(' variable_decl_list_empty ')' '{' instruction_list_empty '}'
    {
        $$ = &driver.env().create<MethodDeclNode>();
        $$->name = *$5;
        $$->body.addAll(*$10);
        $$->kind = naked;
        $$->scope = scope_class;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @11.last_line;
        $$->last_column = @11.last_column;
        
        $3->destroy();
        $5->destroy();
        $7->destroy();
        $10->destroy();
    }
|   ABSTRACT '<' type_list_empty '>' ID '(' variable_decl_list_empty ')' ';'
    {
        $$ = &driver.env().create<MethodDeclNode>();
        $$->name = *$5;
        $$->kind = abstract;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @9.last_line;
        $$->last_column = @9.last_column;
        
        $3->destroy();
        $5->destroy();
        $7->destroy();
    }
|   '<' error '}'
|   GLOBAL '<' error '}'
|   NAKED '<' error '}'
|   ABSTRACT '<' error ';'
;


// EXPRESSIONS
expression:
    NUMBER
    {
        driver.debug() << "-- NUMBER ("<<(int)$1<<")\n";
    }
|   STRING
    {
        driver.debug() << "-- STRING ("<<*$1<<")\n";
    }
|   THIS
    {
        driver.debug() << "-- THIS\n";
    }
|   variable_expr
    {
        driver.debug() << "-- expression (1)\n";
    }
|   assignment_expr
    {
        driver.debug() << "-- expression (2)\n";
    }
|   method_call_expr
    {
        driver.debug() << "-- expression (3)\n";
    }
|   '(' expression ')'
    {
        driver.debug() << "-- expression (4)\n";
    }
;

expression_list:
    expression
|   expression_list ',' expression
;

expression_list_empty:
    // empty
|   expression_list
;

variable_expr:
    ID
    {
        driver.debug() << "-- variable_expr (1)\n";
    }
|   expression DOT ID
    {
        driver.debug() << "-- variable_expr (2)\n";
    }
;

assignment_expr:
    variable_expr EQUAL expression
    {
        driver.debug() << "-- assignment_expr (1)\n";
    }
;

method_call_expr:
    expression DOT ID '(' expression_list_empty ')'
    {
        driver.debug() << "-- method_call_expr (1)\n";
    }
;


// INSTRUCTIONS
instruction:
    expression ';'
    {
        driver.debug() << "-- instruction (1)\n";
        $$ = 0;
    }
|   variable_initializer_inst ';'
    {
        driver.debug() << "-- instruction (3)\n";
        $$ = 0;
    }
|   return_inst ';'
    {
        driver.debug() << "-- instruction (4)\n";
        $$ = 0;
    }
|   inline_pasm_inst ';'
    {
        driver.debug() << "-- instruction (5)\n";
        $$ = $1;
    }
|   '{' instruction_list_empty '}'
    {
        driver.debug() << "-- instruction (6)\n";
        $$ = 0;
    }
|   error ';'
;

instruction_list_empty:
    //empty
    {
        $$ = &driver.env().create<LinkedList<InstructionNode>>();
    }
|   instruction_list_empty instruction
    {
        $$ = $1;
        if ($2) { $$->add(*$2); }
    }
;

return_inst:
    RETURN '<' expression_list_empty '>'
|   RETURN expression
|   RETURN
;

variable_initializer_inst:
    variable_decl EQUAL expression
|   variable_decl COLON_EQUAL expression
|   '<' variable_decl_list '>' EQUAL method_call_expr
|   '<' variable_decl_list '>' COLON_EQUAL method_call_expr
;

inline_pasm_inst:
    PASM '(' STRING ')'
    {
        InlinePasmInstructionNode &node = driver.env().create<InlinePasmInstructionNode>();
        node.pasm = *$3;
        $3->destroy();
        $$ = &node;
        
        $$->first_line = @1.first_line;
        $$->first_column = @1.first_column;
        $$->last_line = @4.last_line;
        $$->last_column = @4.last_column;
    }
;

//=
