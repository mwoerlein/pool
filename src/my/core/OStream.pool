/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace my::core;

class OStream extends Object {
    
    abstract [OStream] printChar(int c);
    
    [OStream] printInt(int i) {
        return this._printInt(i, 10, 0);
    }
    
    [OStream] printHex(int i) {
        return this._printHex(i, 0);
    }
    
    [OStream] printCString(cstring s) {
        int buf = 0;
        __pasm__("", {"%eax": s} , {"%eax": buf});
        return this._printCStringBuffer(buf);
    }
    
    // TODO: use generic method instead of plain Object?
    [OStream] print(__any__ o) {
        Object obj = this.rt().cast(Object:CLASSNAME, o);
        return obj.printToStream(this);
    }
    
    [OStream] printNewline() {
        return this.printChar('\n');
    }

    // private
    [OStream] _printInt(int d, int base, int pad) {
        if (d < 0) {
            this.printChar('-');
            d = -d;
        }
        return this._printUInt(d, base, pad);
    }
    
    [OStream] _printUInt(int d, int base, int pad) {
        int buf_size := 21;
        int p = 0;
        __pasm__("movl %esp, %ebx; subl %eax, %esp", {"%eax": buf_size} , {"%ebx": p});
        
        this._putChar(--p, 0); // string terminator
        int padded_start = p - pad;
        
        /*  Divide D by BASE until D == 0. */
        { // TODO: use do {...} while, if available
            int remainder = d % base;
            if (remainder < 10) {
                this._putChar(--p, remainder + '0');
            } else {
                this._putChar(--p, remainder + 'a' - 10);
            }
        }
        while (d /= base) {
            int remainder = d % base;
            if (remainder < 10) {
                this._putChar(--p, remainder + '0');
            } else {
                this._putChar(--p, remainder + 'a' - 10);
            }
        }
        while (p > padded_start) {
            this._putChar(--p, '0');
        }
    
        this._printCStringBuffer(p);
        __pasm__("addl %eax, %esp", {"%eax": buf_size});
        return this;
    }
    
    [OStream] _printHex(int d, int pad) {
        if (d < 0) {
            this.printChar('-');
            d = -d;
        }
        return this._printUHex(d, pad);
    }
    
    [OStream] _printUHex(int d, int pad) {
        return this.printChar('0').printChar('x')._printUInt(d, 16, pad);
    }
    
    [OStream] _printCStringBuffer(int buffer) {
        int c = 0;
        while (c = this._getChar(buffer++)) {
            this.printChar(c);
        }
        return this;
    }
    
    [] _putChar(int pointer, int c) {
        __pasm__("movb %bl, (%eax)", {"%eax": pointer, "%ebx": c});
    }
    
    [int] _getChar(int pointer) {
        int c = 0;
        __pasm__("movb (%eax), %bl", {"%eax": pointer, "%ebx": c} , {"%ebx": c});
        return c;
    }
    
}
