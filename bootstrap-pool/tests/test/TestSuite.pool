/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace test;

use sys::core::String;
use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyMap;
use sys::runtime::Runtime;
use sys::stream::OStream;

class TestSuite extends sys::core::Object {

    AnyMap knownTests;
    TestOutput output;
    
    [] __init() {
        knownTests = this.createOwn(AnyMap:CLASSNAME);
        knownTests.init(13);
        output = this.createOwn(TestOutput:CLASSNAME);
        
        sys::__Tests__:setup(this);
        pool::__Tests__:setup(this);
    }
    
    [] register(cstring classname, cstring testname) {
        String cn = this.createOwn(String:CLASSNAME);
        cn.assignCString(classname);
        knownTests.set(cn, this.rt().getClassByCName(testname));
    }
    
    [int] runAll() {
        AnyIterator all = knownTests.keys();
        int success = this.runMany(all);
        all.destroy();
        return success;
    }
    
    [int] runMany(AnyIterator classnames) {
        int success = true;
        while (classnames.hasNext()) { if (!this.run(classnames.next())) { success = false; } }
        return success;
    }
    
    [int] run(String classname) {
        OStream err = this.rt().err();
        sys::runtime::Class testClass = knownTests.get(classname);
        if (!testClass) {
            err.printCString("No testcase registered for class '").print(classname).printCString("'").printNewline();
            return false;
        }
        
        TestRuntime testRt = this.rt().createInstance(TestRuntime:CLASSNAME);
        TestCase test = testRt.init(0x800000).createInstanceAs(testClass.getCName(), TestCase:CLASSNAME);
        if (!test) {
            err.printCString("Invalid testcase registered for class '").print(classname).printCString("'").printNewline();
            return false;
        }
        
        int availableBytes = testRt.getAllocator().getAvailableBytes();
        test.setOutput(output).runAll();
        int success = test.getResult() && test.checkMemory(availableBytes);
        
        test.destroy();
        testRt.destroy();
        return success;
    }
}
