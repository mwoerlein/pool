/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace test;

use sys::core::String;
use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyMap;
use sys::runtime::Runtime;
use sys::stream::OStream;

class TestSuite extends sys::core::utils::Owner, sys::log::Logger {

    AnyMap knownTests;
    
    [] __init() {
        this._initOwner();
        this._initLogger();
        knownTests = this.createOwn(AnyMap:CLASSNAME);
        knownTests.init(13);
        
        this.register(sys::core::Boolean:CLASSNAME, sys::core::BooleanTest:CLASSNAME);
    }
    [] __destruct() {
        this._destructLogger();
        this._destructOwner();
    }
    
    [] register(cstring classname, cstring testname) {
        knownTests.set(this.createOwnStringFromCString(classname), this.rt().getClass(testname));
    }
    
    [int] runAll() {
        AnyIterator all = knownTests.keys();
        int success = this.runMany(all);
        all.destroy();
        return success;
    }
    
    [int] runMany(AnyIterator classnames) {
        int success = true;
        while (classnames.hasNext()) { if (!this.run(classnames.next())) { success = false; } }
        return success;
    }
    
    [int] run(String classname) {
        OStream err = this.rt().err();
        sys::runtime::Class testClass = knownTests.get(classname);
        if (!testClass) {
            err.printCString("No testcase registered for class '").print(classname).printCString("'").printNewline();
            return false;
        }
        
        TestRuntime testRt = this.rt().createInstance(TestRuntime:CLASSNAME);
        TestCase test = testRt.init(0x3000).createInstanceAs(testClass.getCName(), TestCase:CLASSNAME);
        if (!test) {
            err.printCString("Invalid testcase registered for class '").print(classname).printCString("'").printNewline();
            return false;
        }
        
        test.init(this.logger());
        int availableBytes = testRt.getAllocator().getAvailableBytes();
        test.runAll();
        int success = test.getResult(); 
        if (success && (availableBytes != testRt.getAllocator().getAvailableBytes())) {
            err.printCString(testClass.getCName()).printCString(" failed: Memory hole detected").printNewline();
            success = false;
        }
        test.destroy();
        testRt.destroy();
        return success;
    }
}
