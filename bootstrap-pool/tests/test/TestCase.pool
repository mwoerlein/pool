/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace test;

use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyMap;
use sys::core::String;
use sys::memory::Address;
use sys::stream::OStream;

class TestCase extends sys::core::Object {

    abstract [] runAll();
    
    cstring currentTest;
    int availableBytes;
    int result;
    int caseResult;
    TestOutput tout;
    
    [] __init() {
        availableBytes = 0;
        result = true;
        caseResult = true;
    }
    [TestCase] setOutput(TestOutput output) { tout = output; return this; }
    
    [int] getResult() { return result; }
    
    [] start(cstring test) {
        currentTest = test;
        availableBytes = this.rt().getAllocator().getAvailableBytes();
    }
    
    [int] fail(cstring message) {
        this.printError(tout.error(), message);
        tout.printFail();
        availableBytes = 0;
        result = false;
        return false; 
    }
    
    [int] skip() {
        this.printMessage(tout.info(), " skipped!");
        tout.printSkip();
        availableBytes = 0;
        return true;
    }
    
    [int] success() {
        if (availableBytes && !this.checkMemory(availableBytes)) { return false; }
        this.printMessage(tout.info(), " successful!");
        tout.printSuccess();
        availableBytes = 0;
        return true;
    }
    
    [] startCases(cstring test) {
        currentTest = test;
        availableBytes = this.rt().getAllocator().getAvailableBytes();
        caseResult = true;
    }
    
    [int] failCase(OStream message) {
        this.printMessage(tout.error(), " failed: ").print(message);
        caseResult = false;
        result = false;
        return false; 
    }
    
    [int] successCase() {
        return true;
    }
    
    [int] successCases() {
        if (!caseResult) { tout.printFail(); availableBytes = 0; return false; }
        return this.success();
    }
    
    [int] checkMemory(int bytes) {
        return (bytes == this.rt().getAllocator().getAvailableBytes()) || this.fail("Memory hole detected");
    }
    
    [OStream] printError(OStream stream, cstring message) {
        return this.printMessage(stream, " failed: ").printCString(message);
    }
    
    [OStream] printMessage(OStream stream, cstring message) {
        stream.printCString(this.getClass().getCName());
        if (availableBytes) { stream.printChar(':').printCString(currentTest).printChar('(').printChar(')'); }
        return stream.printCString(message);
    }
    
    [int] isNull(__any__ a) {
        return !a;
    }
    
    [int] isSame(__any__ a, __any__ b) {
        return Address:compare(a, b) == 0;
    }
}
