/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::asm::test;

use pool::asm::Parser;
use pool::asm::CompilationUnit;
use pool::asm::i386::Compiler;
use sys::core::String;
use sys::stream::IStream;

class TestCase extends test::TestCase {
    
    [int] failUnit(CompilationUnit unit) {
        unit.flushErrors(this.printMessage(tout.error(), " failed:").printNewline());
        tout.printFail();
        availableBytes = 0;
        result = false;
        return false; 
    }
    
    [TestData] newTestData(cstring name) {
        this.start(name);
        TestData data = this.rt().createInstance(TestData:CLASSNAME);
        return data.setName(name);
    }
    
    [int] runTest(TestData data) {
        Buffer buffer = this.rt().createInstance(Buffer:CLASSNAME);
        {
            CompilationUnit unit = this.parseAndCompile(data.getInput(), data.getName(), data.getStartAddress());
            if (unit.hasErrors()) { return this.failUnit(unit); }
        
            buffer.clear();
            buffer.print(unit.getNodeList());
            if (!buffer.equals(data.getPretty())) { return this.fail("invalid pretty"); }
        
            buffer.clear();
            unit.rawToStream(buffer.ostream());
            if (!buffer.equals(data.getBinary())) {
                int ret = this.fail("invalid binary");
                sys::stream::OStream notice = tout.notice();
                data.getBinary().hexDumpToStream(notice.printCString("expected:").printNewline(), 16).printNewline();
                buffer.hexDumpToStream(notice.printCString("got:").printNewline(), 16);
                return ret;
            }
            
            unit.destroy();
        }
        if (data.hasExplicitPretty()) { // test stable compilation of pretty-printed code
            CompilationUnit unit = this.parseAndCompile(data.getPretty(), data.getName(), data.getStartAddress());
            if (unit.hasErrors()) { return this.failUnit(unit); }
        
            buffer.clear();
            buffer.print(unit.getNodeList());
            if (!buffer.equals(data.getPretty())) { return this.fail("invalid stable pretty"); }
        
            buffer.clear();
            unit.rawToStream(buffer.ostream());
            if (!buffer.equals(data.getBinary())) {
                int ret = this.fail("invalid stable binary");
                sys::stream::OStream notice = tout.notice();
                data.getBinary().hexDumpToStream(notice.printCString("expected:").printNewline(), 16).printNewline();
                buffer.hexDumpToStream(notice.printCString("got:").printNewline(), 16);
                return ret;
            }
            
            unit.destroy();
        }
        buffer.destroy();
        data.destroy();
        return this.success();
    }
    
    [CompilationUnit] parseAndCompile(Buffer input, String name, int startAddress) {
        CompilationUnit unit = this.rt().createInstance(CompilationUnit:CLASSNAME);
        Parser parser = this.rt().createInstance(Parser:CLASSNAME);
        Compiler compiler = this.rt().createInstance(Compiler:CLASSNAME);
        
        IStream in = input.toIStream();
        parser.parse(in, name, unit);
        in.destroy();
        if (unit.hasErrors()) { return unit; }
        
        compiler.prepare(unit);
        if (unit.hasErrors()) { return unit; }
        
        compiler.compile(unit);
        if (unit.hasErrors()) { return unit; }
        
        compiler.finalize(unit, startAddress);
        if (unit.hasErrors()) { return unit; }
        
        compiler.destroy();
        parser.destroy();
        return unit;
    }
}
