/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::compiler::ast::visitor;

use pool::compiler::test::TestStorage;
use pool::storage::Mimetype;

class SignatureResolverTest extends pool::compiler::test::TestCase {

    [] runAll() {
        this.testMultiInheritage();
        this.testDirectCyclicClassHierarchyValidation();
        this.testIndirectCyclicClassHierarchyValidation();
        this.testValidCyclicClassDependencies();
    }
    
    [int] testMultiInheritage() {
        TestStorage t = this.newTestStorage("testMultiInheritage");
        t.in("my::A", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("class A extends B {")
            .line("  [] methA() {")
            .line("     this.methB();")
            .line("     this.methC();")
            .line("     this.methD();")
            .line("     this.methE();")
            .line("  }")
            .line("}");
        t.in("my::B", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("class B extends C, D {")
            .line("  [] methB() {")
            .line("     this.methC();")
            .line("     this.methD();")
            .line("     this.methE();")
            .line("  }")
            .line("}");
        t.in("my::C", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("use my::A;")
            .line("class C extends E {")
            .line("  [] methC() {")
            .line("     A a = null;")
            .line("     if (a) { a.methB(); }")
            .line("     this.methE();")
            .line("  }")
            .line("}");
        t.in("my::D", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("class D extends E {")
            .line("  [] methD() {")
            .line("     this.methE();")
            .line("  }")
            .line("}");
        t.in("my::E", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("class E {")
            .line("  [] methE() { }")
            .line("}");
        
        t.prepare("my::C");
        
        return this.runTest(t);
    }
    
    [int] testDirectCyclicClassHierarchyValidation() {
        TestStorage t = this.newTestStorage("testDirectCyclicClassHierarchyValidation");
        t.in("my::A", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("class A extends A { }");
        
        t.expectCompileError("my::A: cyclic class hierarchy detected!")
            .expectCompileError("my::A failed");
        t.prepare("my::A");
        
        return this.runTest(t);
    }
    
    [int] testIndirectCyclicClassHierarchyValidation() {
        TestStorage t = this.newTestStorage("testIndirectCyclicClassHierarchyValidation");
        t.in("my::A", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("class A extends B { }");
        t.in("my::B", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("class B extends A { }");
        
        t.expectCompileError("my::A: cyclic class hierarchy detected!")
            .expectCompileError("my::A failed");
        t.prepare("my::A");
        
        return this.runTest(t);
    }
    
    [int] testValidCyclicClassDependencies() {
        TestStorage t = this.newTestStorage("testValidCyclicClassDependencies");
        t.in("my::A", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("class A extends B {")
            .line("  int active := true;")
            .line("}");
        t.in("my::B", Mimetype:POOL_SRC)
            .line("namespace my;")
            .line("use my::A as Sub;")
            .line("class B {")
            .line("  [Sub] useSub(A a) { if (Sub:active) { return a; } else { return null; } }")
            .line("}");
            
        t.prepare("my::A").prepare("my::B");
        
        return this.runTest(t);
    }
}
