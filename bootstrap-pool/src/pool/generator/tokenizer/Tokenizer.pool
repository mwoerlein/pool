/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::generator::tokenizer;

use pool::generator::regex::node::CharSet;

use pool::generator::tokenizer::dfa::Action;
use pool::generator::tokenizer::dfa::ActionIterator;
use pool::generator::tokenizer::dfa::DFA;
use pool::generator::tokenizer::dfa::State;
use pool::generator::tokenizer::dfa::Type;

use sys::core::String;
use sys::core::anycollection::AnyIterator;
use sys::memory::Address;
use sys::stream::IStream;
use sys::stream::OStream;

class Tokenizer extends sys::core::anycollection::AnyIterator {

    LocationAwareIStream input;
    String tmp;
    String inputName;
    int startLine;
    int startColumn;
    int curLine;
    int curColumn;
    
    Token nextToken;
    Token eoi;
    
    abstract [int] _detectToken();
    
    [Tokenizer] init(IStream in, String inName) {
        input = this.rt().createInstance(LocationAwareIStream:CLASSNAME);
        input.init(in);
        inputName = inName;
        tmp = this.rt().createInstance(String:CLASSNAME);
        eoi = this.rt().createInstance(Token:CLASSNAME);
        eoi.init(tmp, Token:EOI);
        return this;
    }
    [] __destruct() {
        input.destroy();
        tmp.destroy();
        eoi.destroy();
    }
    
    [int] hasNext() {
        return this._fillToken();
    }
    
    [__all__] next() {
        if (this._fillToken()) {
            Token ret = nextToken;
            nextToken = null;
            return Address:convert(ret);
        }
        return Address:convert(eoi);
    }
    [int] _fillToken() {
        if (nextToken) { return true; }
        startLine = input.getLine();
        startColumn = input.getColumn();
        return this._detectToken();
    }
    
    [] _consume() {
        curLine = input.getLine();
        curColumn = input.getColumn();
        tmp.printChar(input.readChar());
    }
    [] _skip() {
        curLine = input.getLine();
        curColumn = input.getColumn();
        input.readChar();
    }
    [] _push(int c) {
        tmp.printChar(c);
    }
    [int] _error(cstring msg) {
        this.rt().err().printCString(msg).printNewline();
        return false;
    }
    
    [int] _setNextToken(Token t, int type) {
        nextToken = t.init(tmp, type).setLocation(inputName, startLine, startColumn, curLine, curColumn);
        tmp.clear();
        return true;
    }
    
    // TODO: extract model based src-code generation
    global [] genClassFromDFA(DFA d, cstring ns, OStream out) {
        State init = d.getInitState();
        out.printCString(<"
/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
">).printNewline();
        out.printCString("namespace ").printCString(ns).printCString("::token;").printNewline();
        out.printNewline();
        out.printCString("class Tokenizer extends pool::generator::tokenizer::Tokenizer {").printNewline();
        out.printNewline();
        out.printCString("    [int] _detectToken() {").printNewline();
        if (init) {
            out.printCString("        int state = ").printInt(init.getId()).printChar(';').printNewline();
            out.printCString("        while (!input.isEmpty()) {").printNewline();
            out.printCString("            int c = input.peekChar();").printNewline();
            
            out.printCString("            if (state == ").printInt(init.getId()).printCString(") {").printNewline();
            Tokenizer:_genRulesForState(init, out);
            
            {
                AnyIterator sit = d.states();
                while (sit.hasNext()) {
                    State s = sit.next();
                    if (s.getId() != init.getId()) {
                        out.printCString("            } else if (state == ").printInt(s.getId()).printCString(") {").printNewline();
                        Tokenizer:_genRulesForState(s, out);
                    }
                }
                sit.destroy();
            }
            out.printCString("            } else {").printNewline();
            out.printCString("                this.rt().err().printCString(\"Unexpected state: \").printInt(state).printNewline();").printNewline();
            out.printCString("                return false;").printNewline();
            out.printCString("            }").printNewline();
            out.printCString("        }").printNewline();
        }
        out.printNewline();
        {
            AnyIterator sit = d.states();
            int pos = 0;
            while (sit.hasNext()) {
                State s = sit.next();
                Action terminal = s.getTerminalAction();
                if (terminal) {
                    if (pos++ == 0) {
                        out.printCString("       ");
                    } else {
                        out.printCString("        else");
                    }
                    out.printCString(" if (state == ").printInt(s.getId()).printChar(')');
                    this._genBlockForAction(terminal, out);
                }
            }
            sit.destroy();
        }
        
        out.printCString("        return false;").printNewline();
        out.printCString("    }").printNewline();
        out.printNewline();
        out.printCString(<"
    [int] _buildToken(int type) {
        Token t = this.rt().createInstance(Token:CLASSNAME);
        return this._setNextToken(t.token(), type);
    }
">).printNewline();
        out.printCString("}").printNewline();
    }
    
    global [] _genRulesForState(State s, OStream out) {
        int pos = 0;
        ActionIterator ait = s.actions();
        while ((pos >= 0) && ait.hasNext()) {
            [CharSet cs, Action a] := ait.next();
            if (pos++ == 0) {
                out.printCString("               ");
            } else {
                out.printCString("                else");
            }
            if (cs.isSingle()) {
                out.printCString(" if (c == ").escapeChar(cs.getSingle()).printChar(')');
            } else if (cs.isAll()) {
                // abort after this action
                pos = -1;
            } else {
                out.printCString(" if (");
                int i = 0;
                int cur = 0;
                while (cur < 256) {
                    if (cs.hasChar(cur)) {
                        if (i++) { out.printCString(" || "); }
                        if (cs.hasChar(cur+1) && cs.hasChar(cur+2)) {
                            int start = cur;
                            // skip consecutive characters
                            while (cs.hasChar(++cur)) {}
                            out.printCString("((c >= ").escapeChar(start).printCString(") && (c <= ").escapeChar(--cur).printCString("))");
                        } else {
                            out.printCString("(c == ").escapeChar(cur).printChar(')');
                        }
                    }
                    cur++;
                }
                out.printChar(')');
            }
            this._genBlockForAction(a, out);
        }
        Action terminal = s.getTerminalAction();
        if (terminal && (pos != -1)) {
            if (pos == 0) {
                out.printCString("               ");
            } else {
                out.printCString("                else");
            }
            this._genBlockForAction(terminal, out);
        }
        ait.destroy();
    }
    
    global [] _genBlockForAction(Action a, OStream out) {
        out.printCString(" {");
        if (a.isError()) {
            out.printCString(" return this._error(").escapeString(a.getError()).printCString(");");
        } else {
            if (a.isPush()) { out.printCString(" this._push(").escapeChar(a.getPush()).printCString(");"); }
            if (a.isSkip()) { out.printCString(" this._skip();"); }
            if (a.isShift()) { out.printCString(" this._consume();"); }
            if (a.isGoto()) { out.printCString(" state = ").printInt(a.getTarget().getId()).printChar(';'); }
            if (a.isReturn()) { out.printCString(" return this._buildToken(Token:").print(a.getType().getName()).printCString(");"); }
        }
        out.printCString(" }").printNewline();
    }
}
