/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::generator::tokenizer::dfa;

use pool::generator::CharSet;
use pool::generator::Symbol;

use pool::generator::regex::fa::NFA as RegexNFA;
use pool::generator::regex::fa::NFASet as RegexNFASet;

use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyList;
use sys::core::anycollection::AnyMap;
use sys::core::anycollection::AnyMapEntryIterator;

class DFA extends Factory {

    AnyMap symbols; // AnyMap<RegexNFA, Symbol>
    AnyList states; // ArrayList<State>
    State initState;
    RegexNFA overall;

    [] __init() {
        symbols = this.createOwn(AnyMap:CLASSNAME);
        states = this.createOwn(AnyList:CLASSNAME);
        
        overall = symbols.createOwn(RegexNFA:CLASSNAME);
        Symbol errorSymbol = symbols.createOwn(Symbol:CLASSNAME);
        symbols.set(overall, errorSymbol.setCName("ERROR").setId(-2));
    }
    
    [AnyIterator] symbols() { return symbols.values(); }
    [AnyIterator] states() { return states.iterator(); }
    [State] getInitState() { return initState; }
        
    [DFA] _addTerminal(Symbol symbol, RegexNFA nfa) {
        overall.addEps(nfa);
        symbols.own(nfa);
        symbols.set(nfa.getTerminal(), symbol);
        return this;
    }
    
    [DFA] _buildActions() {    
        Symbol errorSymbol = symbols.get(overall);
        
        // HashMap<State, HashMap<State, CharSet>>
        AnyMap transitions = this.rt().createInstance(AnyMap:CLASSNAME);
        AnyMap candidates = this.rt().createInstance(AnyMap:CLASSNAME);
        {
            AnyList ws = this.rt().createInstance(AnyList:CLASSNAME);
            RegexNFASet initSet = candidates.createOwn(RegexNFASet:CLASSNAME);
            initState = candidates.createOwn(State:CLASSNAME);
            candidates.set(overall.epsClosure(initSet), initState);
            ws.add(initSet);
            
            while (ws.size()) {
                RegexNFASet set = ws.shift();
                State state = candidates.get(set);
                state.setTerminalSymbol(this._findSymbol(set));

                // initialize transition table for current state
                AnyMap trns = transitions.createOwn(AnyMap:CLASSNAME);
                transitions.set(state, trns);
                
                int c = 0;
                while (c < 256) {
                    RegexNFASet movedSet = set.move(c);
                    if (movedSet) {
                        State movedState = candidates.get(movedSet);
                        if (movedState) {
                            // already known state => use existing set
                            movedSet.destroy();
                        } else {
                            // create new state
                            candidates.own(movedSet);
                            movedState = candidates.createOwn(State:CLASSNAME);
                            candidates.set(movedSet, movedState);
                            ws.add(movedSet);
                        }
                        
                        // insert transition: state --c-> movedState
                        CharSet cs = trns.get(movedState);
                        if (!cs) {
                            cs = trns.createOwn(CharSet:CLASSNAME);
                            trns.set(movedState, cs);
                        }
                        cs.setChar(c);
                    }
                    c++;
                }
            }
            
            ws.destroy();
        }
        {
            AnyMapEntryIterator sourceIt = transitions.entries();
            while (sourceIt.hasNext()) {
                [State source, AnyMap trn] = sourceIt.nextPair();
                Symbol terminal = source.getTerminalSymbol();
                
                if (trn.isEmpty() && terminal) {
                    // terminal states will be inlined
                } else {
                    source.setId(states.size());
                    states.add(source);
                    states.own(source);
                    
                    if (terminal) {
                        source.addTerminalAction(this.ret(terminal));
                    } else {
                        source.addTerminalAction(this.ret(errorSymbol));
                    }
                    
                    AnyMapEntryIterator destIt = trn.entries();
                    while (destIt.hasNext()) {
                        [State dest, CharSet cs] = destIt.nextPair();
                        AnyMap destTrn = transitions.get(dest);
                        Symbol destTerminal = dest.getTerminalSymbol();
                        if (destTrn.isEmpty() && destTerminal) {
                            source.addAction(cs, this.sret(destTerminal));
                            source.own(cs);
                        } else {
                            source.addAction(cs, this.sgoto(dest));
                            source.own(cs);
                        }
                    }
                    destIt.destroy();
                }
            }
            sourceIt.destroy();
        }
        candidates.destroy();
        transitions.destroy();
        return this;
    }
    
    [Symbol] _findSymbol(RegexNFASet set) {
        AnyMapEntryIterator tit = symbols.entries();
        while (tit.hasNext()) {
            [RegexNFA nfa, Symbol s] = tit.nextPair();
            if (set.contains(nfa)) {
                tit.destroy();
                return s;
            }
        }
        tit.destroy();
        return null;
    }
    
}
