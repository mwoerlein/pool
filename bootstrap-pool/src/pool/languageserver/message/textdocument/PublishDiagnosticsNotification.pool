/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::languageserver::message::textdocument;

use pool::languageserver::message::Notification;

use json::value::ArrayValue;
use json::value::ObjectValue;
use json::value::StringValue;
use json::value::NumberValue;

use sys::core::String;
use sys::stream::OStream;

class PublishDiagnosticsNotification extends Notification {

    cstring METHOD := "textDocument/publishDiagnostics";

    StringValue uri;
    NumberValue version;
    ArrayValue diagnostics;
    int lineOffset;
    
    [] __init() {
        this.diagnostics = this.createOwn(ArrayValue:CLASSNAME);
        this.lineOffset = 0;
    }
    
    [PublishDiagnosticsNotification] setUri(String uri) {
        this.uri = this.createOwn(StringValue:CLASSNAME);
        this.uri.assignString(uri);
        return this;
    }
    [PublishDiagnosticsNotification] setVersion(int version) {
        this.version = this.createOwn(NumberValue:CLASSNAME);
        this.version.setInteger(version);
        return this;
    }
    [PublishDiagnosticsNotification] setLineOffset(int lineOffset) {
        this.lineOffset = lineOffset;
        return this;
    }
    
    [Diagnostic] newDiagnostic(int startLine, int startChar, int endLine, int endChar) {
        Diagnostic d = this.createOwn(Diagnostic:CLASSNAME);
        diagnostics.add(d._initDiagnostic(startLine + lineOffset, startChar, endLine + lineOffset, endChar).jsonValue());
        return d;
    }
    
    [String] getUri() { if (uri) { return uri.string(); } return null; }
    [int] getVersion() { if (version) { return version.toInt(); } return -1; }
    [int] hasDiagnostics() { return !diagnostics.isEmpty(); }

    [int] hasParams() { return true; }
    [Notification] initParamsFromJson(ObjectValue params) {
        ObjectValue textDocument = params.cgetObjectValue("textDocument");
        if (textDocument) {
            uri = textDocument.cgetStringValue("uri");
            version = textDocument.cgetNumberValue("version");
        }
        // TODO parse diagnostics
        
        return this.notification();
    }
    [OStream] paramsJsonToStream(OStream stream) {
        stream.printChar('{');
            diagnostics.jsonToStream(stream.printCString("\"diagnostics\":"));
            if (uri) { uri.jsonToStream(stream.printCString(",\"uri\":")); }
            if (version) { version.jsonToStream(stream.printCString(",\"version\":")); }
        return stream.printChar('}');
    }

    [OStream] methodJsonToStream(OStream stream) { return stream.printChar('\"').printCString(METHOD).printChar('\"'); }
    
}
