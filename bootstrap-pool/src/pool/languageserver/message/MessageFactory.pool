/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::languageserver::message;

use pool::languageserver::message::lifecycle::InitializeRequest;
use pool::languageserver::message::lifecycle::InitializeResponse;
use pool::languageserver::message::lifecycle::InitializedNotification;
use pool::languageserver::message::lifecycle::ShutdownRequest;

use pool::languageserver::message::textdocument::DidCloseNotification;
use pool::languageserver::message::textdocument::DidOpenNotification;
use pool::languageserver::message::textdocument::PublishDiagnosticsNotification;

use pool::languageserver::message::window::ShowMessageNotification;

use json::value::ObjectValue;
use json::value::StringValue;
use sys::core::String;
use sys::stream::OStream;

class MessageFactory extends sys::core::Object {
    
    [Message] fromJsonObject(ObjectValue obj) {
        if (!obj) { return null; }
        
        Notification not = this.notificationFromJsonObject(obj);
        if (not) { return not.message(); }
        
        Request req = this.requestFromJsonObject(obj);
        if (req) { return req.message(); }
        
        // TODO: how to determine response types from json? Requires "known/open requests"-Map i.E. to match via id?
        
        return null;
    }
    
    [Notification] notificationFromJsonObject(ObjectValue obj) {
        String method = obj.cgetString("method");
        if (!method) { return null; }
        
        // determine notification
        Notification not = null;
        if (method.cequals(InitializedNotification:METHOD)) { not = this.createInitializedNotification().notification(); }
        if (method.cequals(DidOpenNotification:METHOD)) { not = this.createDidOpenNotification().notification(); }
        if (method.cequals(DidCloseNotification:METHOD)) { not = this.createDidCloseNotification().notification(); }
        if (method.cequals(PublishDiagnosticsNotification:METHOD)) { not = this.createPublishDiagnosticsNotification().notification(); }
        if (method.cequals(ShowMessageNotification:METHOD)) { not = this.createShowMessageNotification().notification(); }
        
        // init params
        if (not && not.hasParams()) { not.initParamsFromJson(obj.cgetObjectValue("params")); }
        return not;
    }
    
    [Request] requestFromJsonObject(ObjectValue obj) {
        String method = obj.cgetString("method");
        String id = obj.cgetString("id");
        if (!method || !id) { return null; }
        
        // determine request
        Request req = null;
        if (method.cequals(InitializeRequest:METHOD)) { req = this.createInitializeRequest(id).request(); }
        if (method.cequals(ShutdownRequest:METHOD)) { req = this.createShutdownRequest(id).request(); }
        
        // init params
        if (req && req.hasParams()) { req.initParamsFromJson(obj.cgetObjectValue("params")); }
        return req;
    }
    
    [Response] createErrorResponse(Message m, int code) {
        Response resp = this.rt().createInstance(Response:CLASSNAME);
        Request req = null;
        if (m) { req = m.asRequest(); }
        if (req) { resp.setRequestId(req.getRequestId()); } else { resp.setCRequestId("unknown"); }
        resp.errorMessage();
        return resp.setErrorCode(code);
    }
    
    // lifecycle    
    [InitializeRequest] createInitializeRequest(String id) {
        InitializeRequest req = this.rt().createInstance(InitializeRequest:CLASSNAME);
        req.setRequestId(id);
        return req;
    }
    [InitializeResponse] createInitializeResponse(InitializeRequest req) {
        InitializeResponse resp = this.rt().createInstance(InitializeResponse:CLASSNAME);
        resp.setRequestId(req.getRequestId());
        return resp;
    }
    [InitializedNotification] createInitializedNotification() { return this.rt().createInstance(InitializedNotification:CLASSNAME); }
    
    [ShutdownRequest] createShutdownRequest(String id) {
        ShutdownRequest req = this.rt().createInstance(ShutdownRequest:CLASSNAME);
        req.setRequestId(id);
        return req;
    }
    
    // text document
    [DidOpenNotification] createDidOpenNotification() { return this.rt().createInstance(DidOpenNotification:CLASSNAME); }
    [DidCloseNotification] createDidCloseNotification() { return this.rt().createInstance(DidCloseNotification:CLASSNAME); }
    [PublishDiagnosticsNotification] createPublishDiagnosticsNotification() { return this.rt().createInstance(PublishDiagnosticsNotification:CLASSNAME); }
    
    // window
    [ShowMessageNotification] createShowMessageNotification() { return this.rt().createInstance(ShowMessageNotification:CLASSNAME); }
}
