/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::asm::ast::operand;

use pool::asm::ast::OperandNode;
use sys::core::String;
use sys::stream::OStream;

class IndirectNode extends OperandNode {

    OperandNode base;
    OperandNode index;
    OperandNode displacement;
    int scale;

    [] __init() { this._initIndirectNode(); }
    [] _initIndirectNode() {
        scale = 1;
        this._initOperandNode();
    }

    [] __destruct() { this._destructIndirectNode(); }
    [] _destructIndirectNode() {
        this._destructOperandNode(); 
    }
    
    [IndirectNode] setBase(OperandNode r) {
        this.base = this.own(r);
        return this;
    }
    [OperandNode] getBase() { return base; }
    
    [IndirectNode] setIndex(OperandNode r) {
        this.index = this.own(r);
        return this;
    }
    [OperandNode] getIndex() { return index; }
    
    [IndirectNode] setDisplacement(OperandNode v) {
        this.displacement = this.own(v);
        return this;
    }
    [OperandNode] getDisplacement() { return displacement; }
    
    [IndirectNode] setScale(int s) {
        this.scale = s;
        return this;
    }
    [int] getScale() { return scale; }
    
    [OStream] printToStream(OStream stream) {
        if (displacement && !base && !index) {
            // memory indirect
            return stream.printChar('(').print(displacement).printChar(')');
        }
        
        // register indirect
        if (displacement) { stream.print(displacement); }
        stream.printChar('(');
        if (base) { stream.print(base); }
        if (index) {
            stream.printChar(',').print(index);
            if (scale > 1) {
                stream.printChar(',').printInt(scale);
            }
        }
        return stream.printChar(')');
    }

}
