/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::asm::i386;

use pool::asm::CompilationUnit;
use pool::asm::Value;

use sys::core::utils::Bitset;
use sys::memory::Address;
use sys::stream::OStream;

class Instruction extends pool::asm::Instruction {

    int pre1; int pre2; int pre3; int pre4;
    int op1; int op2; int op3;
    
    int addr;
    int data;
    int operandSize;
    int preparePos;
    int pos;
    int size;
    
    [Instruction] setOperation(int op1, int op2, int op3) {
        this.op1 = op1;
        this.op2 = op2;
        this.op3 = op3;
        return this;
    }
    
    [Instruction] setOperandSize(int size) {
        operandSize = size;
        return this;
    }
    
    [int] prepare(int pos) {
        this.pos = this.preparePos = pos;
        int mode = block.getCode().getMode();
        addr = node.getAddr(mode);
        data = node.getData(mode);
        return this.size = this.prepareInstruction();
    }

    [int] prepareInstruction() {
        return this.approximateSize();
    }
    
    [int] approximateSize() {
        // TODO: approximate specific instruction size
        // 4xpre 3xop 1xmodrm 1xsib 4xdisp 4ximm
        return 17;
    }
    
    [int] compile(int pos) {
        CompilationUnit unit = this.block.getUnit();
        this.pos = pos;
        // TODO: validate generic and specific instruction properties
        // TODO: prepare prefix nodes, normalize operands, ...
        if (o1) { o1 = o1.compile(unit); }
        if (o2) { o2 = o2.compile(unit); }
        if (o3) { o3 = o3.compile(unit); }
        
        if (data != 0 && data != 8 && data != 16 && data != 32) {
            this.newError().printCString("invalid data: ").printInt(data);
        }
        if (addr != 0 && addr != 8 && addr != 16 && addr != 32) {
            this.newError().printCString("invalid addr: ").printInt(addr);
        }
        if (operandSize != 0 && operandSize != 8 && operandSize != 16 && operandSize != 32) {
            this.newError().printCString("invalid operandSize: ").printInt(operandSize);
        }
        return this.size = this.compileInstruction();
    }
    
    [int] compileInstruction() {
        return this.approximateSize();
    }
    
    [] finalize(int startAddress) {
        pos += startAddress;
    }
    
    [int] getStart() { return pos; }
    [int] getEnd() { return pos + size; }
    
    [int] requiresOperandSizeOverride(int operandSize) {
        return (operandSize == 16 && data == 32) || (operandSize == 32 && data == 16);
    }
    
    [int] requiresAddressSizeOverride(int addressSize) {
        int mode = block.getCode().getMode();
        return (addressSize == 16 && mode == 32) || (addressSize == 32 && mode == 16);
    }
    
    [int] approximateOffset(int position, int posIncr) {
        if (position > pos) {
            return position - preparePos - posIncr;
        } else {
            return position - pos - posIncr;
        }
    }
    
    [OStream] rawOperationToStream(OStream stream) {
        if (pre1) { stream.printChar(pre1); }
        if (pre2) { stream.printChar(pre2); }
        if (pre3) { stream.printChar(pre3); }
        if (pre4) { stream.printChar(pre4); }
        stream.printChar(op1);
        if (op1 == 0x0F) {
            stream.printChar(op2);
            if (op2 == 0x38 || op2 == 0x3A) {
                stream.printChar(op3);
            }
        }
        return stream;
    }
    
    [OStream] rawValueToStream(OStream stream, int val, int size) {
        if (size == 8) {
            return stream
                .printChar(Bitset:and(val, 0xff));
        } else if (size == 16) {
            return stream
                .printChar(Bitset:and(val, 0xff))
                .printChar(Bitset:and(Bitset:rshift(val,  8), 0xff));
        } else if (size == 32) {
            return stream
                .printChar(Bitset:and(val, 0xff))
                .printChar(Bitset:and(Bitset:rshift(val,  8), 0xff))
                .printChar(Bitset:and(Bitset:rshift(val, 16), 0xff))
                .printChar(Bitset:and(Bitset:rshift(val, 24), 0xff));
        }
        return stream;
    }
}
