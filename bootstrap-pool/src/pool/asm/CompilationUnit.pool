/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::asm;

use pool::asm::ast::NodeList;
use pool::asm::ast::Node;

use pool::asm::ast::operand::FormulaNode;
use pool::asm::ast::operand::IdentifierNode;
use pool::asm::ast::operand::IndirectNode;
use pool::asm::ast::operand::RegisterNode;

use pool::asm::value::IdentifierValue;
use pool::asm::value::NumberValue;
use pool::asm::value::NumericOperation;
use pool::asm::value::PositionValue;
use pool::asm::value::StringValue;
use pool::asm::value::TextOperation;

use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyList;
use sys::core::anycollection::AnyMap;
use sys::core::String;
use sys::core::utils::Owner;
use sys::stream::OStream;

class CompilationUnit extends AnyList, Owner { // LinkedList<CompilationBlock>
    
    NodeList nodeList;
    AnyList warnings; // LinkedList<String>
    AnyList errors; // LinkedList<String>
    AnyMap values; // HashMap<String, Value>

    [] __init() {
        sys::runtime::Runtime rt = this.rt();
        nodeList = rt.createInstance(NodeList:CLASSNAME);
        warnings = rt.createInstance(AnyList:CLASSNAME);
        errors = rt.createInstance(AnyList:CLASSNAME);
        values = rt.createInstance(AnyMap:CLASSNAME);
        values.init(13);
        this._initOwner();
    }

    [] __destruct() {
        warnings.destroy();
        errors.destroy();
        values.destroy();
        nodeList.destroy();
        this.clear();
        this._destructOwner();
    }
    
    [NodeList] getNodeList() {
        return nodeList;
    }
    
    [] addDefinition(String id, Value v) {
        values.set(this.createOwnStringFromString(id), v);
    }
    [int] hasDefinition(String id) { return values.has(id); }
    [int] hasPosition(String id) {
        return values.has(id) && this.rt().cast(PositionValue:CLASSNAME, values.get(id));
    }
    [Value] getValue(String id) { return values.get(id); }
    
    [AnyIterator] definitionIds() { return values.keys(); }
    
    [CompilationBlock] addBlock(int mode) {
        CompilationBlock block = this.last();
        if (!block || !block.isEmpty()) {
            block = this.createOwn(CompilationBlock:CLASSNAME);
            this.add(block); 
        }
        block.init(mode, this);
        return block;
    }
    
    [AnyIterator] blocks() { return this.iterator(); }
    
    [Value] _buildNumberValue(int n) {
        NumberValue num = this.createOwn(NumberValue:CLASSNAME);
        num.init(n);
        return num.value();
    }
    
    [Value] _buildIdentifierValue(IdentifierNode n) {
        IdentifierValue id = this.createOwn(IdentifierValue:CLASSNAME);
        id.init(n);
        return id.value();
    }
    
    [Value] _buildStringValue(String n) {
        StringValue str = this.createOwn(StringValue:CLASSNAME);
        str.init(n);
        return str.value();
    }
    
    [Value] _buildTextOperation(FormulaNode n, Value o1, int op, Value o2) {
        TextOperation o = this.createOwn(TextOperation:CLASSNAME);
        o.init(n, o1, op, o2);
        return o.value();
    }
    
    [Value] _buildNumericOperation(FormulaNode n, Value o1, int op, Value o2) {
        NumericOperation o = this.createOwn(NumericOperation:CLASSNAME);
        o.init(n, o1, op, o2);
        return o.value();
    }
    
    [Value] _buildPositionValue(Instruction i) {
        PositionValue value = this.createOwn(PositionValue:CLASSNAME);
        value.init(i);
        return value.value();
    }

    [int] hasErrors() {
        return errors.size() > 0;
    }
    
    [String] newError() {
        String err = this.createOwn(String:CLASSNAME);
        errors.add(err);
        return err;
    }
    
    [String] newWarning() {
        String warn = this.createOwn(String:CLASSNAME);
        warnings.add(warn);
        return warn;
    }
    
    [int] flushErrors(OStream stream) {
        {
            AnyIterator it = warnings.iterator();
            while (it.hasNext()) { stream.print(it.next()).printNewline(); }
            it.destroy();
            warnings.clear();
        }
        
        if (errors.size() == 0) {
            return false;
        }
        
        {
            AnyIterator it = errors.iterator();
            while (it.hasNext()) { stream.print(it.next()).printNewline(); }
            it.destroy();
            errors.clear();
        }
        return true;
    }
    
    [OStream] rawToStream(OStream stream) {
        {
            AnyIterator bit = this.blocks();
            while (bit.hasNext()) {
                CompilationBlock block = bit.next();
                {
                    AnyIterator iit = block.instructions();
                    while (iit.hasNext()) {
                        Instruction inst = iit.next();
                        inst.rawToStream(stream);
                    }
                    iit.destroy();
                }
            }
            bit.destroy();
        }
        return stream;
    }
}
