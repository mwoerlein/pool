/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::asm;

use pool::asm::ast::NodeList;
use pool::asm::ast::Node;

use pool::asm::ast::operand::FormulaNode;
use pool::asm::ast::operand::IdentifierNode;
use pool::asm::ast::operand::IndirectNode;
use pool::asm::ast::operand::RegisterNode;

use pool::asm::value::IdentifierValue;
use pool::asm::value::NumberValue;
use pool::asm::value::NumericOperation;
use pool::asm::value::PositionValue;
use pool::asm::value::StringValue;
use pool::asm::value::TextOperation;

use pool::storage::Mimetype;

use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyList;
use sys::core::anycollection::AnyMap;
use sys::core::String;
use sys::core::utils::Owner;
use sys::core::utils::PropertyContainer;
use sys::log::Logger;
use sys::stream::OStream;

class CompilationUnit extends Owner, Logger {
    
    NodeList nodeList;
    AnyList blocks; // LinkedList<CompilationBlock>
    AnyMap values; // HashMap<String, Value>

    [] __init() {
        nodeList = this.createOwn(NodeList:CLASSNAME);
        blocks = this.createOwn(AnyList:CLASSNAME);
        values = this.createOwn(AnyMap:CLASSNAME);
        this._initLogger();
    }
    [] __destruct() {
        this._destructLogger();
    }
    
    [NodeList] getNodeList() {
        return nodeList;
    }
    
    [] addDefinition(String id, Value v) {
        if (values.has(id)) {
            this.newError().printCString("re-declaring '").print(id).printChar('\'');
            return;
        }
        values.set(this.createOwnStringFromString(id), v);
    }
    [int] hasDefinition(String id) { return values.has(id); }
    [int] hasPosition(String id) {
        return values.has(id) && this.rt().cast(PositionValue:CLASSNAME, values.get(id));
    }
    [Value] getValue(String id) { return values.get(id); }
    
    [AnyIterator] definitionIds() { return values.keys(); }
    
    [CompilationBlock] addBlock(int mode) {
        CompilationBlock block = blocks.last();
        if (!block || !block.isEmpty()) {
            block = this.createOwn(CompilationBlock:CLASSNAME);
            blocks.add(block); 
        }
        block.init(mode, this);
        return block;
    }
    
    [AnyIterator] blocks() { return blocks.iterator(); }
    
    [Value] _buildNumberValue(int n) {
        NumberValue num = this.createOwn(NumberValue:CLASSNAME);
        num.init(n);
        return num.value();
    }
    
    [Value] _buildIdentifierValue(IdentifierNode n) {
        IdentifierValue id = this.createOwn(IdentifierValue:CLASSNAME);
        id.init(n);
        return id.value();
    }
    
    [Value] _buildStringValue(String n) {
        StringValue str = this.createOwn(StringValue:CLASSNAME);
        str.init(n);
        return str.value();
    }
    
    [Value] _buildTextOperation(FormulaNode n, Value o1, int op, Value o2) {
        TextOperation o = this.createOwn(TextOperation:CLASSNAME);
        o.init(n, o1, op, o2);
        return o.value();
    }
    
    [Value] _buildNumericOperation(FormulaNode n, Value o1, int op, Value o2) {
        NumericOperation o = this.createOwn(NumericOperation:CLASSNAME);
        o.init(n, o1, op, o2);
        return o.value();
    }
    
    [Value] _buildPositionValue(Instruction i) {
        PositionValue value = this.createOwn(PositionValue:CLASSNAME);
        value.init(i);
        return value.value();
    }

    [OStream] rawToStream(OStream stream) {
        {
            AnyIterator bit = this.blocks();
            while (bit.hasNext()) {
                CompilationBlock block = bit.next();
                {
                    AnyIterator iit = block.instructions();
                    while (iit.hasNext()) {
                        Instruction inst = iit.next();
                        inst.rawToStream(stream);
                    }
                    iit.destroy();
                }
            }
            bit.destroy();
        }
        return stream;
    }
    
    [OStream] printHeadersToStream(OStream stream, cstring version, PropertyContainer classProperties) {
        stream.printCString("/*[meta]").printNewline();
        stream.printCString("mimetype = ").printCString(Mimetype:TYPE_BIN_X86).printNewline();
        
        stream.printCString("[pool]").printNewline();
        stream.printCString("version = ").printCString(version).printNewline();
        if (classProperties) {
            stream.printCString("class = true").printNewline();
            if (classProperties.hasCStringProperty("pool.name")) {
                stream.printCString("name = ").print(classProperties.getCStringProperty("pool.name")).printNewline();
            } else if (classProperties.hasCStringProperty("pool_source.unit")) {
                stream.printCString("name = ").print(classProperties.getCStringProperty("pool_source.unit")).printNewline();
            }
        }
        stream.printCString("*/").printNewline();
        return stream;
    }
    
    // @deprecated: TODO use new Logger/LoggerAware methods directly
    [OStream] newError() { return this.error(); }
    [OStream] newWarning() { return this.warn(); }
    [int] hasErrors() { return this.has(Logger:ERROR); }
    [int] flushErrors(OStream stream) {
        int ret = this.has(Logger:ERROR);
        this.flush();
        return ret;
    }
}
