/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::command;

use pool::generator::parser::Parser;
use pool::generator::parser::grammar::Parser as GrammarParser;
use pool::generator::parser::grammar::Grammar;
use pool::generator::regex::Tokenizer;
use pool::storage::Mimetype;

use sys::core::String;
use sys::stream::IStream;
use sys::stream::OStream;

class ParserGen extends linux::Command {

    [] __init() {
        this._initCommand();
    }
    
    [] __destruct() {
        this._destructCommand();
    }
    
    [] configure() {
        this.setName("parsergen")
            .setVersion("0.1.0")
            .setUsage(<"
Parser Generator.

    Usage:
      parsergen -c <dir>

    Options:
      -h --help                     Show this screen.
      --version                     Show version.
      -c <dir> --classpath <dir>    TBD
      TODO
">);
        this.registerOptionAlias('h', "help")
            .registerOptionAlias('c', "classpath")
        ;
    }

    [int] run() {
        OStream out = this.rt().out();
        if (this.hasOption("help")) {
            out.printCString(usage).printNewline();
            return 0;
        }
        if (this.hasOption("version")) {
            out.printCString(name).printChar(' ').printCString(version).printNewline();
            return 0;
        }
        if (!this.hasValueOption("classpath")) {
            out.printCString(usage).printNewline();
            return -1;
        }
        
        this.refreshTokenizerAndParser(out, "pool/generator/regex", "Parser.gram");
        this.refreshTokenizerAndParser(out, "pool/generator/parser/grammar", "Parser.gram");
        this.refreshTokenizerAndParser(out, "pool/asm", "Parser.gram");
        out.printCString("finished").printNewline();
        return 0;
    }
    
    [] refreshTokenizerAndParser(OStream out, cstring dir, cstring filename) {
        String source = this.rt().createInstance(String:CLASSNAME);
        source.printCString(dir).printChar('/').printCString(filename);
        String grammarFilename = this.rt().createInstance(String:CLASSNAME);
        grammarFilename.print(this.getValueOption("classpath")).printChar('/').print(source);
        IStream inputFile = this.openFile(grammarFilename, linux::fs::File:MODE_READ).istream();
        GrammarParser parser = this.rt().createInstance(GrammarParser:CLASSNAME);
        
        linux::fs::DirectoryPoolStorage storage = this.rt().createInstance(linux::fs::DirectoryPoolStorage:CLASSNAME);
        storage.setBaseDir(this.getValueOption("classpath"));
        
        out.printCString("refresh from ").print(grammarFilename).printNewline();
        Grammar gram = parser.parse(inputFile, grammarFilename, source);
        
        parser.destroy();
        inputFile.destroy();
        grammarFilename.destroy();
        source.destroy();

        if (gram) {
            out.printCString("generating").printNewline();
            {
                String fqn = this.rt().createInstance(String:CLASSNAME);
                fqn.print(gram.getNamespace()).printCString("::").print(gram.getTokenizerClassname());
                OStream out = storage.writeElement(fqn, Mimetype:POOL_SRC);
                Tokenizer:genClassFromGrammar(gram, out);
                out.destroy();
                fqn.destroy();
            }
            {
                String fqn = this.rt().createInstance(String:CLASSNAME);
                fqn.print(gram.getNamespace()).printCString("::").print(gram.getClassname());
                OStream out = storage.writeElement(fqn, Mimetype:POOL_SRC);
                Parser:genClassFromGrammar(gram, out);
                out.destroy();
                fqn.destroy();
            }
            gram.destroy();
        }

    }
    
    // methods
    __entry__ [int] entry(int argc, int argv) {
        ParserGen pg = linux::Runtime:buildRuntime().createInstance(ParserGen:CLASSNAME);
        return pg.execute(argc, argv);
    }
}
