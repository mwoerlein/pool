/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::compiler;

use pool::compiler::ast::NamedType;
use pool::compiler::ast::node::declaration::ClassDeclNode;
use pool::compiler::ast::node::declaration::StructDeclNode;
use pool::compiler::ast::node::declaration::TranslationUnitNode;
use pool::compiler::ast::visitor::PrettyPrinter;
use pool::compiler::ast::visitor::TypeReferenceResolver;
use pool::compiler::parser::Parser;
use pool::storage::ClassPathStorage;
use pool::storage::PoolStorage;

use sys::core::String;
use sys::core::anycollection::AnyList;
use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyMapEntryIterator;
use sys::log::Logger;
use sys::store::StorageElement;
use sys::stream::OStream;

class Compiler extends pool::compiler::ast::TypeManager {

    ClassPathStorage classPath;
    PoolStorage out;
    Parser parser;
    PrettyPrinter pp;
    TypeReferenceResolver resolveTypeRefs;
    AnyList preparedUnits; // LinkedList<TranslationUnitNode>
    
    [] __init() {
        this._initTypeManager();
        parser = this.createOwn(Parser:CLASSNAME);
        classPath = this.createOwn(ClassPathStorage:CLASSNAME);
        preparedUnits = this.createOwn(AnyList:CLASSNAME);
        resolveTypeRefs = this.createOwn(TypeReferenceResolver:CLASSNAME);
        resolveTypeRefs.setTypeManager(this.typeManager());
    }
    [Compiler] initLogger(Logger logger) {
        this.setLogger(logger);
        parser.setLogger(logger);
        resolveTypeRefs.setLogger(logger);
        return this;
    }
    
    [Compiler] addClassPath(PoolStorage pool) { classPath.add(this.own(pool)); return this; }
    [Compiler] setOutput(PoolStorage pool) { out = this.own(pool); return this; }
    [Compiler] setPrettyOutput(PoolStorage pool) {
        pp = this.createOwn(PrettyPrinter:CLASSNAME);
        pp.setStorage(pool);
        return this;
    }
    
    [int] hasErrors() { return logger.has(Logger:ERROR); }
    
    [] prepare(String fqn) {
        NamedType type = _namedTypes.get(fqn);
        if (type) {
            // reuse already initialized unit
            preparedUnits.add(type.getUnitNode());
        } else {
            TranslationUnitNode unit = this._initialize(fqn);
            if (unit) { preparedUnits.add(unit); } else { this._failUnit(fqn); }
        }
    }
    
    [] skip(String fqn) {
        this.note().print(fqn).printCString(" skipped");
    }
    
    [] compilePrepared() {
        AnyIterator it = preparedUnits.iterator();
        while (!this.hasErrors() && it.hasNext()) {
            TranslationUnitNode unit = it.next();
            AnyIterator cit = unit.getClasses().iterator();
            while (!this.hasErrors() && cit.hasNext()) {
                ClassDeclNode cd = cit.next();
                if (!this._compile(unit.getName(), cd)) { this._failUnit(unit.getName()); }
            }
            cit.destroy();
        }
        it.destroy();
    }
    
    [] compileAll() {
        int success = true;
        AnyMapEntryIterator it = _namedTypes.entries();
        while (!this.hasErrors() && it.hasNext()) {
            [String fqn, NamedType t] := it.nextPair();
            ClassDeclNode cd = t.getClassDeclNode();
            if (cd && !this._compile(fqn, cd)) { this._failUnit(cd.getName()); }
        }
        it.destroy();
    }
    
    [NamedType] getNamedType(String fqn) {
        if (!_namedTypes.has(fqn) && !this._initialize(fqn)) {
            return null;
        }
        return _namedTypes.get(fqn);
    }
    
    [TranslationUnitNode] _initialize(String fqn) {
        this.info().printCString("initialize ").print(fqn);
        StorageElement elem = classPath.getSourceClass(fqn);
        if (!elem) {
            this.error().printCString("declaration of '").print(fqn).printCString("' not found!");
            return null;
        }
        TranslationUnitNode unit = this.createOwn(TranslationUnitNode:CLASSNAME);
        this._debugStep(fqn, "parse");
        parser.parse(unit.init(elem, fqn));
        if (this.hasErrors()) { unit.destroy(); return null; }
        
        if (pp) {
            this._debugStep(fqn, "pretty print");
            pp.visit(unit.node());
        }
        
        this._debugStep(fqn, "resolve type references");
        resolveTypeRefs.visit(unit.node());
        
        return unit;
    }
    
    [int] _compile(String fqn, ClassDeclNode node) {
        this.note().printCString("compile ").print(fqn);
/*        
        this._debugStep(fqn, "resolve methods");
        resolveMethods.visit(node);
        if (this.hasErrors()) { return false; }
        
        this._debugStep(fqn, "resolve instructions");
        resolveInstructions.visit(node);
        if (this.hasErrors()) { return false; }
        
        this._debugStep(fqn, "generate PIR");
        generatePIR.visit(node);
        this._debugStep(fqn, "generate binary");
        generatePASM.visit(node);
        if (this.hasErrors()) { return false; }
        
        this._debugStep(fqn, "pretty print");
        if (pp) { pp.visit(node.getUnitNode()); }
*/
        if (this.hasErrors()) { return false; }
        return true;
    }
    
    [] _failUnit(String fqn) {
        this.error().print(fqn).printCString(" failed");
    }
    
    [] _debugStep(String fqn, cstring step) {
        this.debug().printChar(' ').printChar(' ').print(fqn).printChar(':').printChar(' ').printCString(step);
    }
}
