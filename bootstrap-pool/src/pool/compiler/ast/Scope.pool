/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::compiler::ast;

use pool::compiler::ast::node::declaration::ClassDeclNode;
use pool::compiler::ast::node::declaration::StructDeclNode;
use pool::compiler::ast::node::declaration::TranslationUnitNode;
use pool::compiler::ast::node::declaration::MethodDeclNode;
use pool::compiler::ast::node::declaration::VariableDeclNode;
use pool::compiler::ast::node::instruction::BlockInstNode;

use pool::compiler::ast::scope::BlockScope;
use pool::compiler::ast::scope::ClassScope;
use pool::compiler::ast::scope::InstanceScope;
use pool::compiler::ast::scope::MethodScope;
use pool::compiler::ast::scope::StructScope;
use pool::compiler::ast::scope::UnitScope;
use pool::compiler::ast::scope::VariableScope;

use sys::core::String;
use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyMap;
use sys::core::anycollection::AnyMapEntryIterator;
use sys::stream::OStream;

class Scope extends sys::core::Object {

    abstract [OStream] printDebugName(OStream stream);
    
    int STATE_REGISTERED                :=  5;
    int STATE_RESOLVING_TYPE_REFERENCES := 10;
    int STATE_TYPE_REFERENCES_RESOLVED  := 15;
    int STATE_VALIDATING_HIERARCHY      := 20;
    int STATE_HIERARCHY_INVALID         := 23;
    int STATE_HIERARCHY_VALIDATED       := 25;
    int STATE_RESOLVING_SIGNATURE       := 30;
    int STATE_RESOLVING_SCOPES          := 33;
    int STATE_SIGNATURE_RESOLVED        := 35;
    int STATE_RESOLVING_INSTRUCTIONS    := 40;
    int STATE_INSTRUCTIONS_RESOLVED     := 45;
    
    Scope _parent;
    int _state;

    AnyMap _namedTypes; // HashMap<String, NamedType>
    AnyMap _methods;    // HashMap<String, MethodScope>
    AnyMap _variables;  // HashMap<String, VariableScope>

    [] __init() { this._initScope(); }
    [] _initScope() {
        _namedTypes = this.createOwn(AnyMap:CLASSNAME);
        _methods = this.createOwn(AnyMap:CLASSNAME);
        _variables = this.createOwn(AnyMap:CLASSNAME);
    }
    
    [Scope] setParent(Scope parent) { _parent = parent; return this; }
    [Scope] getParent() { return _parent; }
    [Scope] setState(int state) { _state = state; return this; }
    [int] getState() { return _state; }

    [TranslationUnitNode] getUnitNode() { if (_parent) { return _parent.getUnitNode(); } return null; }
    [StructDeclNode] getStructDeclNode() { if (_parent) { return _parent.getStructDeclNode(); } return null; }
    [ClassDeclNode] getClassDeclNode() { if (_parent) { return _parent.getClassDeclNode(); } return null; }
    [MethodDeclNode] getMethodDeclNode() { if (_parent) { return _parent.getMethodDeclNode(); } return null; }
    [BlockInstNode] getBlockInstNode() { if (_parent) { return _parent.getBlockInstNode(); } return null; }
    [VariableDeclNode] getVariableDeclNode() { if (_parent) { return _parent.getVariableDeclNode(); } return null; }
    
    [ClassScope, InstanceScope] registerClass(ClassDeclNode node, String alias) {
        ClassScope classScope = _namedTypes.createOwn(ClassScope:CLASSNAME);
        classScope.setClassDeclNode(node).setParent(this).setState(Scope:STATE_REGISTERED);
        InstanceScope instanceScope = classScope.createOwn(InstanceScope:CLASSNAME);
        instanceScope.setClassDeclNode(node).setParent(classScope.scope());
        this.registerNamedType(classScope.namedType(), alias);
        
        node.setClassScope(classScope).setInstanceScope(instanceScope);
        return [classScope, instanceScope];
    }
    [StructScope] registerStruct(StructDeclNode node, String alias) {
        StructScope scope = _namedTypes.createOwn(StructScope:CLASSNAME);
        scope.setStructDeclNode(node).setParent(this).setState(Scope:STATE_REGISTERED);
        this.registerNamedType(scope.namedType(), alias);
        
        node.setStructScope(scope);
        return scope;
    }
    
    [NamedType] registerNamedType(NamedType type, String alias) {
        NamedType old = _namedTypes.set(alias, type);
        if (old) { old.destroy(); }
        return type;
    }
    [NamedType] getNamedType(String name) {
        if (_namedTypes.has(name)) { return _namedTypes.get(name); }
        if (_parent) { return _parent.getNamedType(name); }
        return null;
    }
    
    [MethodScope] registerMethod(MethodDeclNode node) {
        MethodScope scope = _methods.createOwn(MethodScope:CLASSNAME);
        scope.setMethodDeclNode(node).setParent(this);
        // TODO #7: generate method id from name and parameter types
        MethodScope old = _methods.set(node.getName(), scope);
        if (old && this.isSameAs(old._parent)) { old.destroy(); }
        
        node.setMethodScope(scope);
        return scope;
    }
    [] registerMethodsFromScope(Scope source) {
        AnyMapEntryIterator it := source._methods.entries();
        while (it.hasNext()) {
            [String id, MethodScope ms] := it.nextPair();
            MethodScope old := _methods.set(id, ms);
            if (old && this.isSameAs(old._parent)) { old.destroy(); }
        }
        it.destroy();
    }
    [MethodScope] getMethod(String name) {
        if (_methods.has(name)) { return _methods.get(name); }
        if (_parent) { return _parent.getMethod(name); }
        return null;
    }
    [AnyIterator] getMethods() {
        return _methods.iterator();
    }
    
    [VariableScope] registerVariable(VariableDeclNode node) {
        VariableScope scope = _variables.createOwn(VariableScope:CLASSNAME);
        scope.setVariableDeclNode(node).setParent(this);
        VariableScope old = _variables.set(node.getName(), scope);
        if (old && this.isSameAs(old._parent)) { old.destroy(); }
        
        node.setVariableScope(scope);
        return scope;
    }
    [] registerVariablesFromScope(Scope source) {
        AnyMapEntryIterator it := source._variables.entries();
        while (it.hasNext()) {
            [String id, VariableScope vs] := it.nextPair();
            VariableScope old := _variables.set(id, vs);
            if (old && this.isSameAs(old._parent)) { old.destroy(); }
        }
        it.destroy();
    }
    [VariableScope] getVariable(String name) {
        if (_variables.has(name)) { return _variables.get(name); }
        if (_parent) { return _parent.getVariable(name); }
        return null;
    }
    [AnyIterator] getVariables() {
        return _variables.iterator();
    }

    [BlockScope] createBlockScope(BlockInstNode node) {
        BlockScope scope = this.createOwn(BlockScope:CLASSNAME);
        scope.setBlockInstNode(node).setParent(this);
        
        node.setBlockScope(scope);
        return scope;
    }
    
    global [UnitScope] createUnitScope(TranslationUnitNode node) {
        UnitScope scope = node.createOwn(UnitScope:CLASSNAME);
        scope.setUnitNode(node);
        
        node.setUnitScope(scope);
        return scope;
    }
    
    [OStream] printToStream(OStream stream) {
        return this.printDebugName(stream);
    }
    
    // TODO: remove with implicit cast
    [Scope] scope() { return this; }
}
