/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace pool::compiler::ast::node::declaration;

use pool::compiler::ast::Node;
use pool::compiler::ast::node::reference::ClassRefNode;
use pool::compiler::ast::Visitor;
use sys::core::String;
use sys::stream::OStream;

class UseStatementNode extends Node {

    ClassRefNode _name;
    String _alias;

    [] __init() {
        _alias = this.createOwn(String:CLASSNAME);
    }
    
    [UseStatementNode] setName(ClassRefNode name) { _name = this.own(name); return this; }
    [ClassRefNode] getName() { return _name; }
    
    [UseStatementNode] setAlias(String alias) { _alias.assignString(alias); return this; }
    [int] hasAlias() { return _alias.length() > 0; }
    [String] getAlias() { return _alias; }
    
    // NODE
    [int] accept(Visitor visitor) { return visitor.visitUseStatement(this); }
    [OStream] printDebugName(OStream stream) {
        return stream.printCString("use-<").print(_name.getName()).printChar('>');
    }
}
