/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace linux;

use linux::fs::File;
use linux::fs::Directory;

use sys::core::String;
use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyList;
use sys::core::anycollection::AnyMapEntry;
use sys::core::anycollection::AnyMapEntryIterator;
use sys::core::utils::CommandLine;
use sys::core::utils::CommandLineParser;
use sys::core::utils::CommandLineParserTarget;
use sys::memory::Address;
use sys::runtime::Runtime;
use sys::stream::IStream;
use sys::stream::OStream;
use sys::stream::SeekableIOStream;

class Command extends CommandLine {

    cstring name;
    cstring version;
    cstring usage;
    
    cstring _calledCommand;
    
    // methods
    abstract [] configure();
    abstract [int] run();
    
    [] __init() { this._initCommand(); }
    [] _initCommand() {
        this._initCommandLine();
    }
    
    [] __destruct() { this._destructCommand(); }
    [] _destructCommand() {
        this._destructCommandLine();
    }
    
    [int] execute(int argc, int argv) {
        this.configure();
        if (this._parseArgv(argc, argv)) {
            return this.run();
        }
        return -1;
    }

    // protected
    [Command] setName(cstring name) {
        this.name = name;
        return this;
    }
    
    [Command] setVersion(cstring version) {
        this.version = version;
        return this;
    }
    
    [Command] setUsage(cstring usage) {
        this.usage = usage;
        // TODO: implement usage parsing (see http://docopt.org/)
        return this;
    }
    
    [IStream] openStdIn() {
        StdIn in = this.rt().createInstance(StdIn:CLASSNAME);
        return in.istream();
    }
    
    [SeekableIOStream] openFile(String filename, int mode) {
        int buf = 0;
        __pasm__("subl %eax, %esp", {"%eax": filename.length() + 1}, {"%esp": buf});
        SeekableIOStream f = this.openCFile(filename.toCString(buf), mode);
        __pasm__("addl %eax, %esp", {"%eax": filename.length() + 1});
        return f;
    }
    
    [SeekableIOStream] openCFile(cstring filename, int mode) {
        File f = this.rt().createInstance(File:CLASSNAME);
        int r = f._init(filename, mode, 0);
        if (r < 0) {
            f.destroy();
            return null;
        }
        return this.rt().cast(SeekableIOStream:CLASSNAME, f);
    }
    
    [Directory] openDirectory(String classpath) {
        linux::fs::Directory d = this.rt().createInstance(linux::fs::Directory:CLASSNAME);
        return d.setBaseDir(classpath);
    }
    
    // debug
    [OStream] dumpCLToStream(OStream stream) {
        stream.printCString("called command: ").printCString(this._calledCommand).printNewline();
        stream.printCString("options:").printNewline();
        {
            AnyMapEntryIterator it = this._options.entries();
            while (it.hasNext()) {
                AnyMapEntry e = it.next();
                String key = e.getKey();
                String value = e.getValue();
                stream.printChar('-').printChar(' ').print(key);
                if (e.getValue()) {
                    stream.printChar(' ').printChar('=').printChar(' ').print(value);
                }
                stream.printNewline();
            }
            it.destroy();
        }
        {
            AnyMapEntryIterator it = this._listOptions.entries();
            while (it.hasNext()) {
                AnyMapEntry e = it.next();
                String key = e.getKey();
                AnyList list = e.getValue();
                stream.printChar('-').printChar(' ').print(key).printChar(' ').printChar('[');
                AnyIterator lit = list.iterator();
                while (lit.hasNext()) {
                    String value = lit.next();
                    stream.printChar(' ').print(value);
                }
                lit.destroy();
                stream.printChar(' ').printChar(']').printNewline();
            }
            it.destroy();
        }
        stream.printCString("arguments:").printNewline();
        {
            AnyIterator it = this._arguments.iterator();
            while (it.hasNext()) {
                String arg = it.next();
                stream.printChar('-').printChar(' ').print(arg).printNewline();
            }
            it.destroy();
        }
        return stream;
    }
    
    // private
    [int] _parseArgv(int argc, int argv) {
        Runtime rt = this.rt();
        
        _calledCommand = Address:loadString(argv);
        
        // convert argv to List<String>
        AnyList args = rt.createInstance(AnyList:CLASSNAME);
        while (--argc) {
            args.add(this.createOwnStringFromCString(Address:loadString(argv += 4)));
        }
        
        // parse argument list
        CommandLineParser p = rt.createInstance(CommandLineParser:CLASSNAME);
        AnyIterator ai = args.iterator();
        int ret = p.parse(ai, this.target());
        ai.destroy();
        p.destroy();
        args.destroy();
        return ret;
    }
}
