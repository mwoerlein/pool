/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace linux;

use sys::memory::Allocator;
use sys::memory::PageAllocator;
use sys::memory::PagedMemoryManager;
use sys::runtime::Runtime as SysRuntime;
use sys::runtime::DynamicClassStorage;
use sys::stream::OStream;

class Runtime extends sys::runtime::RuntimeBuilder {
    
    // methods
    global [SysRuntime] buildRuntime() {
        return Runtime:_bootstrap(Runtime:CLASSNAME, 0x1000, null);
    }
    
    [SysRuntime] _buildRuntime(__any__ context) {
        SysRuntime bsRt := this.rt();
        LinuxOStream bsLOut := this.rt().createInstance(LinuxOStream:CLASSNAME);
        bsLOut.setForeground(LinuxOStream:BRIGHT_WHITE);
        OStream debug := bsLOut.initOStream(SysCall:FD_STDOUT);
        bsRt.setOut(debug);
        bsRt.setErr(debug);
        Allocator bsAllocator := bsRt.getAllocator();
        
        // bootstrap heap management
        PageAllocator bsPA := bsRt.createInstanceAs(LinuxPageAllocator:CLASSNAME, PageAllocator:CLASSNAME);
        PagedMemoryManager bsPMM := bsRt.createInstance(PagedMemoryManager:CLASSNAME);
        bsPMM.setMinimalPageAllocation(0x40).initFromPageAllocator(bsPA, 1);
        
        SysRuntime rt = bsRt.createSelfcontainedRuntime(SysRuntime:CLASSNAME, bsPMM.allocator(), bsRt.getClassStorage());
        
        // rebuild class storage in runtime
        DynamicClassStorage dcs = rt.createInstance(DynamicClassStorage:CLASSNAME);
        bsRt.getClassStorage().transferClasses(dcs, null);
        rt.setClassStorage(dcs.classStorage());
        dcs.rebuildClasses();
        
        // rebuild heap management in runtime
        PageAllocator pa := rt.createInstanceAs(LinuxPageAllocator:CLASSNAME, PageAllocator:CLASSNAME);
        PagedMemoryManager pmm := rt.createInstance(PagedMemoryManager:CLASSNAME);
        pmm.initFromPagedMemoryManager(pa, bsPMM);
        rt.setAllocator(pmm.allocator());

        // setup further runtime services
        LinuxOStream ls1 = rt.createInstance(LinuxOStream:CLASSNAME);
        rt.setOut(ls1.initOStream(SysCall:FD_STDOUT));
        LinuxOStream ls2 = rt.createInstance(LinuxOStream:CLASSNAME);
        ls2.setForeground(LinuxOStream:BRIGHT_RED);
        rt.setErr(ls2.initOStream(SysCall:FD_STDERR));
        LinuxClock lc = rt.createInstance(LinuxClock:CLASSNAME);
        rt.setClock(lc.clock());
        
        bsLOut.destroy();
        bsPA.destroy();
        bsPMM.destroy();
        
        return rt;
    }
    
}
