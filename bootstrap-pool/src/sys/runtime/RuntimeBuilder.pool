/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace sys::runtime;

use sys::core::Object;
use sys::memory::Address;
use sys::memory::MemoryManager;
use sys::memory::MemoryInfo;
use sys::stream::OStream;

class RuntimeBuilder extends Object {
    
    // methods
    abstract [Runtime] _buildRuntime(__any__ context);
    
    global [Runtime] _bootstrap(cstring builderName, int heapsize, __any__ context) {
        Runtime runtime = null;
        
        // allocate buffer on stack
        int heap = 0;
        __pasm__("subl %eax, %esp", {"%eax": heapsize}, {"%esp": heap});
        MemoryInfo available = Address:cast(heap);
        available.buf = heap;
        available.len = heapsize;
        available.flags = MemoryInfo:FLAGS_AVAILABLE;
        
        ClassDescriptor cdClass := Class:_getClassDesc();
        ClassDescriptor cdRt := Runtime:_getClassDesc();
        ClassDescriptor cdMM := MemoryManager:_getClassDesc();
        ClassDescriptor cdSRCS := StaticResolvedClassStorage:_getClassDesc();
        
        MemoryInfo miClass := MemoryManager:_allocateFromBottom(available, cdClass.inst_tpl_size);
        MemoryInfo miRt := MemoryManager:_allocateFromBottom(available, cdRt.inst_tpl_size);
        MemoryInfo miMM := MemoryManager:_allocateFromBottom(available, cdMM.inst_tpl_size);
        MemoryInfo miSRCS := MemoryManager:_allocateFromBottom(available, cdSRCS.inst_tpl_size);

        [Class iClass, Object oClass] := Runtime:_initTemplate(miClass, cdClass);
        [Runtime iRt, Object oRt] := Runtime:_initTemplate(miRt, cdRt);
        [MemoryManager iMM, Object oMM] := Runtime:_initTemplate(miMM, cdMM);
        [StaticResolvedClassStorage iSRCS, Object oSRCS] := Runtime:_initTemplate(miSRCS, cdSRCS);

        iRt.setRt(iRt);
        iRt.setAllocator(iMM.allocator());
        iRt.setClassStorage(iSRCS.classStorage());
        
        iSRCS.setRt(iRt);
        
        iClass.setRt(iRt);
        iClass.setDesc(cdClass);
        
        iMM.setRt(iRt);
        iMM.__init();
        iMM.initEmpty(available, 0, 0);
        
        iRt.__init();
        iSRCS.__init();
        iClass.__init();

        iMM._appendToUsed(miClass);
        iMM._appendToUsed(miRt);
        iMM._appendToUsed(miMM);
        iMM._appendToUsed(miSRCS);

        // create and init classes
        if (iRt._createClass(cdRt) && iRt._createClass(cdMM) && iRt._createClass(cdSRCS)) {
            // build runtime if no initialization error occurred
            RuntimeBuilder builder := iRt.cast(RuntimeBuilder:CLASSNAME, iRt.createInstance(builderName));
            runtime = builder._buildRuntime(context);
            builder.destroy();
        }

        // free stack
        __pasm__("addl %eax, %esp", {"%eax": heapsize});

        return runtime;
    }
}
