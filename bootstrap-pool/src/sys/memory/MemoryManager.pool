/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace sys::memory;

use sys::runtime::ClassDescriptor;
use sys::stream::OStream;

class MemoryManager extends Allocator {
    
    int MEMORY_LISTS := 4;
    int OPTIMIZE_THRESHOLD := 0x1000;
    
    MemoryInfo available;
    MemoryInfo free;
    int freeCounter;
    MemoryInfo reserved;
    MemoryInfo used;
    MemoryInfoPool nonEmbeddedInfos;
    int allocateIncreasing;
    
    // methods
    [MemoryInfo] allocate(int size) {
        int required = size + MemoryInfo:SIZEOF;
        
        if (Address:compareAddress(required, size) < 0) {
            return this.sizeOverflow(size);
        }
        if (freeCounter >= MemoryManager:OPTIMIZE_THRESHOLD) { this.optimize(); }
        
        // find available memory
        MemoryInfo available = this._findInfoByLen(this.available, required);
        if (Address:compareAddress(available.len, required) < 0) {
            return this.outOfAvailable(size);
        }
        
        if (Address:compareAddress(available.len, required + 2*MemoryInfo:SIZEOF) < 0) {
            // use whole info
            available.buf += MemoryInfo:SIZEOF;
            available.len -= MemoryInfo:SIZEOF;
            
            // remove from available
            this._unlinkMemoryInfo(available);
        } else {
            // split info
            [MemoryInfo used, MemoryInfo remaining] = this._splitAvailableMemoryInfo(available, size);
    
            if (Address:compare(available, remaining)) {
                // replace available by remaining in available list
                this._replaceMemoryInfo(available, remaining);
            } else {
                available = used;
            }
        }
        
        // append to used
        this._appendToUsed(available);
        
        return available;
    }
    
    [] free(MemoryInfo info) {
        MemoryInfo freeInfo = info; 
        int freeInfoPtr = Address:from(freeInfo);
        
        if (!freeInfo || (freeInfo.flags != MemoryInfo:FLAGS_USED)) {
            this.rt().err().printCString("bad free ").printPtr(freeInfoPtr).printNewline();
            return;
        }
        
        // remove from used
        freeInfo.flags = MemoryInfo:FLAGS_AVAILABLE;
        this._unlinkMemoryInfo(freeInfo);
        
        if (freeInfo.buf == freeInfoPtr + MemoryInfo:SIZEOF) {
            // adjust embedded info
            freeInfo.buf = freeInfoPtr;
            freeInfo.len += MemoryInfo:SIZEOF;
        } else {
            freeInfo = this._makeEmbedded(freeInfo);
        }
    
        // collect free infos to merge them into available later
        this._appendMemoryInfo(this.free.prev, freeInfo);
        freeCounter++;
    }
    
    [] optimize() {
        MemoryInfo freeList = this.free;
        if (Address:compare(freeList.next, freeList)) {
            // extract free elements
            MemoryInfo freeHead = freeList.next;
            freeList.next.prev = freeList.prev.next = null;
            freeList.next = freeList.prev = freeList;
            
            // sort free elements
            freeHead = this._sortElements(freeHead);
            
            // extract available elements
            MemoryInfo availableList = this.available;
            MemoryInfo availableHead = availableList.next;
            availableList.next.prev = availableList.prev.next = null;
            availableList.next = availableList.prev = availableList;
            
            // merge free elements and available elements
            availableHead = this._mergeSortedElements(availableHead, freeHead);
            
            // convert available elements into available list
            this._elementsToList(availableHead, availableList);
            
            freeCounter = 0;
        }
    }
    
    [MemoryInfo] getInfo(int addr) {
        // TODO: implement
        return Address:cast(MemoryInfo:NONE);
    }
    
    [int] getAvailableBytes() {
        // merge free memory into available list, before counting available bytes
        this.optimize();
        
        int bytes = 0;
        MemoryInfo cur = available.next;
        while (Address:compare(cur, available)) {
            bytes += cur.len;
            cur = cur.next;
        }
        return bytes;
    }
    
    // errors
    [MemoryInfo] sizeOverflow(int size) {
        this.rt().err().printCString("bad allocate ").printPtr(size).printNewline();
        return Address:cast(MemoryInfo:NONE);
    }
    
    [MemoryInfo] outOfAvailable(int size) {
        this.rt().err().printCString("bad allocate ").printPtr(size).printNewline();
        return Address:cast(MemoryInfo:NONE);
    }
    
    // initializations
    [] initEmpty(MemoryInfo available, int allocateIncreasing, int poolElements) {
        this.allocateIncreasing = allocateIncreasing;
        
        // allocate pool
        int poolSize = MemoryInfoPool:SIZEOF + (poolElements + MEMORY_LISTS)*MemoryInfo:SIZEOF;
        int requiredSize = MemoryInfo:SIZEOF + poolSize;
        if (Address:compareAddress(available.len, requiredSize + MemoryInfo:SIZEOF) < 0) {
            // bail out
            this.rt().err().printCString("too many pool elements for available space\n");
            return;
        }
        [MemoryInfo used, MemoryInfo initialAvailable] = this._splitAvailableMemoryInfo(available, poolSize);
        used.flags = MemoryInfo:FLAGS_USED;
        
        this._initEmptyPool(used, poolElements + MEMORY_LISTS);
        
        this._appendMemoryInfo(this.available.prev, initialAvailable);
        this._appendMemoryInfo(this.used.prev, used);
    }
    
    [] initEmptyFromAllocator(Allocator allocator, int allocateIncreasing, int poolElements) {
        this.allocateIncreasing = allocateIncreasing;
        
        // allocate pool
        int poolSize = MemoryInfoPool:SIZEOF + poolElements*MemoryInfo:SIZEOF;
        MemoryInfo used = allocator.allocate(poolSize);
        if (Address:from(used) == MemoryInfo:NONE) { return; } // bail out
        
        this._initEmptyPool(used, poolElements);
    }
    
    [] initFromMemoryManager(MemoryManager src) {
        this.allocateIncreasing = src.allocateIncreasing;
        this.nonEmbeddedInfos = src.nonEmbeddedInfos;
        this.available = src.available;
        this.free = src.free;
        this.freeCounter = src.freeCounter;
        this.reserved = src.reserved;
        this.used = src.used;
    }
    
    [MemoryInfoPool] _initEmptyPool(MemoryInfo info, int poolElements) {
        MemoryInfoPool pool = Address:cast(info.buf);
        pool.self = info;
        pool.size = poolElements;
        pool.used = 0;
        pool.elements = info.buf + MemoryInfoPool:SIZEOF;
        
        this.nonEmbeddedInfos = pool;
        this.available = this._nextNonEmbeddedInfo(0, 0, MemoryInfo:FLAGS_AVAILABLE);
        this.free      = this._nextNonEmbeddedInfo(0, 0, MemoryInfo:FLAGS_AVAILABLE);
        this.reserved  = this._nextNonEmbeddedInfo(0, 0, MemoryInfo:FLAGS_RESERVED);
        this.used      = this._nextNonEmbeddedInfo(0, 0, MemoryInfo:FLAGS_USED);
        this.freeCounter = 0;
    }
    
    // pool handling
    [MemoryInfo] _nextNonEmbeddedInfo(int buf, int len, int flags) {
        if (nonEmbeddedInfos.used >= nonEmbeddedInfos.size) {
            this.rt().err().printCString("insufficient non-embedded memory infos").printNewline();
            return Address:cast(MemoryInfo:NONE);
        }
        MemoryInfo info = Address:cast(nonEmbeddedInfos.elements + MemoryInfo:SIZEOF*(nonEmbeddedInfos.used++));
        info.buf = buf;
        info.len = len;
        info.flags = flags;
        info.owner = 0;
        info.next = info.prev = info;
        return info;
    }
    
    [MemoryInfo] _makeEmbedded(MemoryInfo nonEmbeddedInfo) {
        // create embedded info
        MemoryInfo info = Address:cast(nonEmbeddedInfo.buf);
        info.buf = nonEmbeddedInfo.buf;
        info.len = nonEmbeddedInfo.len;
        info.flags = nonEmbeddedInfo.flags;
        info.owner = nonEmbeddedInfo.owner;
        
        this._replaceMemoryInfo(nonEmbeddedInfo, info);
        // clear non-embedded info
        nonEmbeddedInfo.buf = 0;
        nonEmbeddedInfo.len = 0;
        nonEmbeddedInfo.next = nonEmbeddedInfo.prev = nonEmbeddedInfo;
        
        return info;
    }
    
    [MemoryInfo, MemoryInfo] _splitAvailableMemoryInfo(MemoryInfo available, int size) {
        if (!this.allocateIncreasing) {
            MemoryInfo info = MemoryManager:_allocateFromBottom(available, size);
            return [info, available];
        }
        
        int required = size + MemoryInfo:SIZEOF;
        int remainingBuf = available.buf + required;
        MemoryInfo remaining = Address:cast(remainingBuf);
        remaining.buf = remainingBuf;
        remaining.len = available.len - required;
        remaining.flags = MemoryInfo:FLAGS_AVAILABLE;
            
        available.buf += MemoryInfo:SIZEOF;
        available.len = size;
        return [available, remaining];
    }
    
    // memory info list handling    
    [MemoryInfo] _findInfoByPtr(MemoryInfo list, int ptr) {
        MemoryInfo cur = list;
        while (Address:compare(cur.next, list) && Address:compareAddress(cur.next.buf, ptr) <= 0) { cur = cur.next; }
        return cur;
    }
    
    [MemoryInfo] _findInfoByLen(MemoryInfo list, int len) {
        MemoryInfo cur = list;
        while (Address:compare(cur.next, list) && Address:compareAddress(cur.len, len) < 0) { cur = cur.next; }
        return cur;
    }
    
    [] _mergeMemoryInfoInto(MemoryInfo list, MemoryInfo info) {
        MemoryInfo prev = this._findInfoByPtr(list, info.buf);
        this._appendMemoryInfo(prev, info);
        MemoryInfo next = info.next;
        
        if (((info.buf + info.len) == next.buf) && Address:compare(next, list)) {
            // merge right
            info.len = (next.buf + next.len) - info.buf; 
            this._unlinkMemoryInfo(next);
            next.flags = 0;
        }
        if (((prev.buf + prev.len) == info.buf) && Address:compare(prev, list)) {
            // merge left
            prev.len = (info.buf + info.len) - prev.buf;
            this._unlinkMemoryInfo(info);
            info.flags = 0;
            
            int prevPtr = Address:from(prev);
            
            if ((prev.buf != prevPtr) && (Address:compareAddress(prev.len, MemoryInfo:SIZEOF) > 0)) {
                prev = this._makeEmbedded(prev);
            }
        }
    }
    
    [] _appendToUsed(MemoryInfo used) {
        used.flags = MemoryInfo:FLAGS_USED;
        //MemoryInfo usedPrev = this._findInfoByPtr(this.used, available.buf); // for sorted used-list
        MemoryInfo usedPrev = this.used.prev;
        this._appendMemoryInfo(usedPrev, used);
    }
    
    // generic info list handling    
    [] _appendMemoryInfo(MemoryInfo info, MemoryInfo newInfo) {
        newInfo.prev = info;
        newInfo.next = info.next;
        newInfo.prev.next = newInfo.next.prev = newInfo;
    }
    
    [] _unlinkMemoryInfo(MemoryInfo info) {
        info.next.prev = info.prev;
        info.prev.next = info.next;
        info.next = info.prev = info;
    }
    
    [] _replaceMemoryInfo(MemoryInfo info, MemoryInfo newInfo) {
        newInfo.next = info.next;
        newInfo.prev = info.prev;
        newInfo.prev.next = newInfo.next.prev = newInfo;
        info.next = info.prev = info;
    }
    
    // merge sort of info elements
    [MemoryInfo] _sortElements(MemoryInfo elements) {
        if (!elements || !elements.next) { return elements; }
        [MemoryInfo a, MemoryInfo b] = this._splitElements(elements);
        return this._mergeSortedElements(this._sortElements(a), this._sortElements(b));
    }
    
    [MemoryInfo, MemoryInfo] _splitElements(MemoryInfo elements) {
        MemoryInfo fast = elements;
        MemoryInfo slow = elements;
        while (fast.next) {
            fast = fast.next;
            if (fast.next) {
                fast = fast.next;
                slow = slow.next;
            }
        }
        MemoryInfo tmp = slow.next;
        slow.next = null; 
        return [elements, tmp];
    }
    
    [MemoryInfo] _mergeSortedElements(MemoryInfo ae, MemoryInfo be) {
        MemoryInfo a = ae;
        MemoryInfo b = be;
        if (!a) { return b; }
        if (!b) { return a; }
        MemoryInfo merged = null;
        MemoryInfo tail = null;
        if (Address:compareAddress(a.buf, b.buf) < 0) {
            merged = tail = a; a = a.next;
        } else {
            merged = tail = b; b = b.next;
        }
        while (a && b) {
            if (Address:compareAddress(a.buf, b.buf) < 0) {
                tail = this._mergeOrAppend(tail, a); a = a.next;
            } else {
                tail = this._mergeOrAppend(tail, b); b = b.next;
            }
        }
        if (a) { this._mergeOrAppend(tail, a).next = a.next; }
        if (b) { this._mergeOrAppend(tail, b).next = b.next; }
        return merged;
    }
    
    [MemoryInfo] _mergeOrAppend(MemoryInfo prev, MemoryInfo info) {
        if ((prev.buf + prev.len) != info.buf) {
            return prev.next = info;
        }
        // merge left
        prev.len = (info.buf + info.len) - prev.buf;
        info.flags = 0;
        
        int prevPtr = Address:from(prev);
        
        if ((prev.buf != prevPtr) && (Address:compareAddress(prev.len, MemoryInfo:SIZEOF) > 0)) {
            prev = this._makeEmbedded(prev);
        }
        return prev;
    }
    
    // debug
    [OStream] dumpToStream(OStream stream, int all) {
        if (Address:compare(available.next, available)) {
            stream.printCString("available:\n");
            MemoryInfo cur = available.next;
            while (Address:compare(cur, available)) {
                MemoryManager:_dumpInfo(cur, stream).printNewline();
                cur = cur.next;
            }
        }
        if (all) {
            if (Address:compare(free.next, free)) {
                stream.printCString("free:\n");
                MemoryInfo cur = free.next;
                while (Address:compare(cur, free)) {
                    MemoryManager:_dumpInfo(cur, stream).printNewline();
                    cur = cur.next;
                }
            }
            if (Address:compare(reserved.next, reserved)) {
                stream.printCString("reserved:\n");
                MemoryInfo cur = reserved.next;
                while (Address:compare(cur, reserved)) {
                    MemoryManager:_dumpInfo(cur, stream).printNewline();
                    cur = cur.next;
                }
            }
            if (Address:compare(used.next, used)) {
                stream.printCString("used:\n");
                MemoryInfo cur = used.next;
                while (Address:compare(cur, used)) {
                    MemoryManager:_dumpInfo(cur, stream);
                    sys::core::Object o = this.rt()._asObject(cur.buf);
                    if (o) {
                        stream.printChar(' ').print(o.getClass().getName());
                    } 
                    stream.printNewline();
                    cur = cur.next;
                }
            }
        }
        
        return stream;
    }
    
    [] _elementsToList(MemoryInfo info, MemoryInfo list) {
        MemoryInfo prev = list;
        while (info) {
            MemoryInfo next = info.next;
            this._appendMemoryInfo(prev, info);
            prev = info;
            info = next;
        }
    }
    
    // global
    global [MemoryInfo] _allocateFromBottom(MemoryInfo available, int size) {
        int required = size + MemoryInfo:SIZEOF;
        int bottomBuf = available.buf + available.len - required;
        MemoryInfo bottom = Address:cast(bottomBuf);
        bottom.buf = bottomBuf + MemoryInfo:SIZEOF;
        bottom.len = size;
            
        available.len -= required;
        return bottom;
    }
    
    global [OStream] _dumpInfo(MemoryInfo info, OStream stream) {
        int infoEnd = info.buf + info.len;
        int infoPtr = Address:from(info);
        return stream.printPtr(info.buf).printChar(':').printPtr(infoEnd)
            .printChar('[').printPtr(info.len).printChar(']')
            .printChar(' ').printChar('(').printPtr(infoPtr).printChar(')');
    }
    
    global [ClassDescriptor] _getClassDesc() {
        ClassDescriptor desc = null;
        __pasm__("movl 8(%ebp), %eax", {}, {"%eax": desc});
        return desc;
    } 
}
