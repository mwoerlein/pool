/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace sys::log;

use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyList;
use sys::stream::OStream;

class Logger extends sys::core::Object {

    int CRITICAL := 6;
    int ERROR    := 5;
    int WARNING  := 4;
    int NOTE     := 3;
    int INFO     := 2;
    int DEBUG    := 1;
    
    AnyList _entries; // LinkedList<LogEntry>
    int _minLevel;
    int _lastMax;
    
    [] __init() { this._initLogger(); }
    [] _initLogger() {
        _entries = this.createOwn(AnyList:CLASSNAME);
        _minLevel = Logger:NOTE;
        _lastMax = 0;
    }
    
    [Logger] setLevel(int level) { _minLevel = level; return this; }
    
    [OStream] crit()  { return this.log(Logger:CRITICAL).ostream(); }
    [OStream] error() { return this.log(Logger:ERROR).ostream(); }
    [OStream] warn()  { return this.log(Logger:WARNING).ostream(); }
    [OStream] note()  { return this.log(Logger:NOTE).ostream(); }
    [OStream] info()  { return this.log(Logger:INFO).ostream(); }
    [OStream] debug() { return this.log(Logger:DEBUG).ostream(); }
    
    [LogEntry] log(int level) {
        LogEntry e = this.rt().createInstance(LogEntry:CLASSNAME);
        _entries.add(e.setLevel(level));
        if (level > _lastMax) { _lastMax = level; }
        return e;
    }
    
    [int] has(int level) { return _lastMax >= level; }
    
    [] flush() {
        AnyIterator it = _entries.iterator();
        while (it.hasNext()) { this.showEntry(it.next()); }
        it.destroy();
        this.clear();
    }
    
    [] clear() {
        _entries.destroyAll();
        _lastMax = 0;
    }
    
    [] showEntry(LogEntry e) {
        int level = e.getLevel();
        if (level < _minLevel) { /* ignore */}
        else if (level < Logger:WARNING) { this.rt().out().print(e).printNewline(); }
        else { this.rt().err().print(e).printNewline(); }
    }
    
    // @deprecated: TODO replace all occurences (in pool::asm) with new logger methods
    [OStream] newError() { return this.error(); }
    [OStream] newWarning() { return this.warn(); }
    [int] hasErrors() { return this.has(Logger:ERROR); }
    [int] flushErrors(OStream stream) {
        int ret = this.has(Logger:ERROR);
        this.flush();
        return ret;
    }
    
    // TODO: remove with implicit cast
    [Logger] logger() { return this; }
}
