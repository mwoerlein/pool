/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace sys::core;

use sys::core::utils::Bitset;
use sys::stream::OStream;

// TODO: extends Comparable<Character> for compare operator support, if available
class Character extends Object {

    int _c;
    
    // methods
    [] assignChar(int c) { _c = Bitset:and(0xff, c); }
    [] assignCharacter(Character c) { _c = c.getChar(); }
    
    [int] getChar() { return _c; }
    [int] getLowerChar() {
        if (_c >= 'A' && _c <= 'Z') {
            return _c - 'A' + 'a';
        }
        return _c;
    }
    [int] getUpperChar() {
        if (_c >= 'a' && _c <= 'z') {
            return _c - 'a' + 'A';
        }
        return _c;
    }
    
    [int] compareChar(int c) { return _c - Bitset:and(0xff, c); }
    [int] compareCharacter(Character c) { return _c - c.getChar(); }
    
    [int] hash() { return _c; }
    [int] equals(__any__ obj) {
        Character c = this.rt().cast(Character:CLASSNAME, obj);
        if (c) {
            return this.compareCharacter(c) == 0;
        }
        return 0;
    }
    
    [OStream] printToStream(OStream stream) {
        return stream.printChar(_c);
    }

}
