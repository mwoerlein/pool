/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace vector::graphics::builder;

use sys::core::Flt;
use sys::core::String;

use vector::graphics::Graphic;
use vector::graphics::path::Path;
use vector::graphics::shape::PathShape;

class Rangnar extends vector::graphics::Builder {
    
    String ravenColor;
    String mystraColor;
    String mystraHighlightColor;
    String lathandarColor;
    String gondColor;
    String gondArrowColor;
    
    int scales;
    
    int cx;
    int cy;
    
    int m1r;
    int m2r; 
    int m3r; 
    int m4r;
    int m5r;
    int m6r;
    int m7r;
    int m8r;
     
    int m1a; 
    int m2a; 
    int m2as; 
    int m3a;
    int m3as;
    int m4a; 
    int m6a;
    int m6as;
    int m7a;
    int m7as;
    
    int lWidth;
    int l1r;
    int l2r;
    int l3r;
    int l4r;
    
    int l2a;
    int l3a;
    int l4a;
    int l5x;
    int l5y;
    int l6x;
    int l6y;
    
    int gWidth;
    int g1r;
    int g2r; 
    int g3r;
    int g4r;
    int g5r;
    int g6r;
    int g7r;
    int g8r;
    int g9r;
    int g10r;
    int g11r;
    int g12r;
    
    int gStep;
    int g1a;
    int g2a;
    int g4a;
    int g4as;
    int g5a;
    int g5as;
    int g6a1;
    int g6a1s;
    int g6a2;
    int g6a2s;
    int g7a;
    int g7as;
    int g8a;
    int g8as;
    int g10a;
    int g10as;
    int g11a;
    int g11as;
     
    [] __init() { this._initRangnar(); }
    [] _initRangnar() {
        _initBuilder(Flt:fromInt(0), Flt:fromInt(50), Flt:fromInt(500), Flt:fromInt(500));
        
        ravenColor = this.createStringFromCString("black");
        scales = false;
    }
    
    [Rangnar] withGond() {
        gondColor = this.createStringFromCString("#FDCC0D");
        gondArrowColor = this.createStringFromCString("#AE6E55");
        return this;
    }
    [Rangnar] withMystra() {
        mystraColor = this.createStringFromCString("#9599BC");
        mystraHighlightColor = this.createStringFromCString("#E0E0E0");
        return this;
    }
    [Rangnar] withLathandar() {
        lathandarColor = this.createStringFromCString("#F0C0B0");
        return this;
    }
    [Rangnar] withScales() { this.scales = true; return this; }
    
    
    [Graphic] buildGraphic() {
        this._initializeCoordinates();
        
        Graphic logo := this._newGraphic();
        
        Path raven := this._addNewFilledPath(logo, ravenColor);
        Path mystra := this._addNewFilledPath(logo, mystraColor);
        Path mystraHighlight := this._addNewFilledPath(logo, mystraHighlightColor);
        Path lathandar := this._addNewFilledPath(logo, lathandarColor);
        Path gondGear := this._addNewFilledPath(logo, gondColor);
        Path gondArrow := this._addNewFilledPath(logo, gondArrowColor);
        
        this._generateRaven(raven);
        this._generateMystra(mystra, mystraHighlight);
        this._generateLathandar(lathandar);
        this._generateGond(gondGear, gondArrow);
        
        return logo;
    }
    
    [] _generateRaven(Path raven) {
        raven
            .moveCTo("-70", "165")
            .lineCTo("-495", "470")
            .lineCTo("-485", "365")
            .lineCTo("-295", "205")
            .lineCTo("-455", "225")
            .lineCTo("-375", "140")
            .lineCTo("-255", "110")
            
            .lineCTo("-335", "60")
            .lineCTo("-290", "35")
            .lineCTo("-205", "55")
            .lineCTo("-290", "-35")
            .lineCTo("-230", "-20")
            .lineCTo("-150", "35")
            .lineCTo("-185", "-50")
            .lineCTo("-150", "-35")
            .lineCTo("-95", "40")
            .lineCTo("-85", "-70")
            .lineCTo("-45", "-50")
            .lineCTo("-50", "-90");
            
        if (this.scales) {
            raven
                .lineCTo("-190", "-95")
                .lineCTo("-190", "-100")
                .lineCTo("-170", "-100")
                .lineCTo("-195", "-152")
                .lineCTo("-165", "-175")
                .lineCTo("-135", "-152")
                .lineCTo("-160", "-100");
        }
             
        raven
            .lineCTo("-50", "-100")
            .lineCTo("-50", "-105")
            .lineCTo("-155", "-220")
            .lineCTo("-110", "-220")
            .lineCTo("-75", "-185")
            .lineCTo("-95", "-235")
            .lineCTo("-85", "-290")
            .lineCTo("-60", "-225")
            .lineCTo("-65", "-320")
            .lineCTo("-35", "-350")
            .lineCTo("-25", "-305")
            
            .lineCTo("0", "-370")
             
            .lineCTo("25", "-305")
            .lineCTo("35", "-350")
            .lineCTo("65", "-320")
            .lineCTo("60", "-225")
            .lineCTo("85", "-290")
            .lineCTo("95", "-235")
            .lineCTo("75", "-185")
            .lineCTo("110", "-220")
            .lineCTo("155", "-220")
            .lineCTo("50", "-105")
            .lineCTo("50", "-100");
            
        if (this.scales) {
            raven
                .lineCTo("160", "-100")
                .lineCTo("135", "-152")
                .lineCTo("165", "-175")
                .lineCTo("195", "-152")
                .lineCTo("170", "-100")
                .lineCTo("190", "-100")
                .lineCTo("190", "-95");
        }
        
        raven
            .lineCTo("50", "-90")
            .lineCTo("45", "-50")
            .lineCTo("85", "-70")
            .lineCTo("95", "40")
            .lineCTo("150", "-35")
            .lineCTo("185", "-50")
            .lineCTo("150", "35")
            .lineCTo("230", "-20")
            .lineCTo("290", "-35")
            .lineCTo("205", "55")
            .lineCTo("290", "35")
            .lineCTo("335", "60")

            .lineCTo("255", "110")
            .lineCTo("375", "140")
            .lineCTo("455", "225")
            .lineCTo("295", "205")
            .lineCTo("485", "365")
            .lineCTo("495", "470")
            .lineCTo("70", "165");
            
        // head
        raven
            .lineCTo("30", "255")
            .lineCTo("25", "275")
            .lineCTo("10", "300")
            .lineCTo("-25", "310")
            .lineCTo("-55", "310")
            .lineCTo("-90", "325")
            .lineCTo("-140", "325")
            .lineCTo("-90", "295")
            .lineCTo("-65", "270")
            .lineCTo("-60", "255")
            .lineCTo("-50", "240")
            .close();
            
        if (this.scales) {
            raven
                .moveCTo("-165", "-100")
                .lineCTo("-140", "-150")
                .lineCTo("-190", "-150")
                .close();
            raven
                .moveCTo("190", "-150")
                .lineCTo("140", "-150")
                .lineCTo("165", "-100")
                .close();
        }
    }
    
    [] _generateMystra(Path star, Path highlight) {
        {
            int moveTo = true;
            int alpha = 0;
            while (alpha < 360) {
                int cur = Flt:fromInt(alpha);
                [int x1, int y1] := Path:getPolarPoint(m1r, Flt:degToRad(cur));
                [int x5, int y5] := Path:getPolarPoint(m5r, Flt:degToRad(cur));
                if (moveTo) {
                    star.moveFltTo(x1, y1);
                    highlight.moveFltTo(x5, y5);
                    moveTo = false;
                } else {
                    star.lineFltTo(x1, y1);
                    highlight.lineFltTo(x5, y5);
                }
                star.arcFltTo(cx, cy, m2r, Flt:add(cur, m2a), Flt:add(cur, m2as), true);
                highlight.arcFltTo(cx, cy, m6r, Flt:add(cur, m6a), Flt:add(cur, m6as), true);
                
                alpha += 45;
            }
            star.close();
            highlight.close();
        }
        {
            int moveTo = true;
            int alpha = 0;
            while (alpha < 360) {
                int cur = Flt:fromInt(alpha);
                [int x, int y] := Path:getPolarPoint(m4r, Flt:degToRad(Flt:add(cur, m4a)));
                [int x8, int y8] := Path:getPolarPoint(m8r, Flt:degToRad(cur));
                star.arcFltTo(cx, cy, m3r, Flt:add(cur, m3a), Flt:add(cur, m3as), false)
                    .lineFltTo(x, y)
                    .close();
                if (moveTo) {
                    highlight.moveFltTo(x8, y8);
                    moveTo = false;
                } else {
                    highlight.lineFltTo(x8, y8);
                }
                highlight.arcFltTo(cx, cy, m7r, Flt:add(cur, m7a), Flt:add(cur, m7as), true);
                
                alpha += 45;
            }
            highlight.close();
        }
    }
    
    [] _generateLathandar(Path outline) {
        outline
            .circleFltTo(cx, cy, l1r, false)
            .close();
        int f0 = Flt:fromInt(0);
        int f45 = Flt:fromInt(45);
        int f45n = Flt:fromInt(-45);
        int f90 = Flt:fromInt(90);
        int f135 = Flt:fromInt(135);
        int f225 = Flt:fromInt(225);
        int f270 = Flt:fromInt(270);
        
        outline
            .arcFltTo(cx, cy, l2r, l2a, Flt:sub(f45, l2a), false)
            .arcFltTo(cx, cy, l3r, Flt:sub(f45, l3a), l3a, true)
            .close();
        outline
            .arcFltTo(cx, cy, l2r, Flt:sub(f90, l2a), Flt:add(f45, l2a), false)
            .arcFltTo(cx, cy, l3r, Flt:add(f45, l3a), Flt:sub(f90, l3a), true)
            .close();
        outline
            .arcFltTo(cx, cy, l2r, Flt:neg(l2a), Flt:sub(l2a, f45), false)
            .arcFltTo(cx, cy, l3r, Flt:sub(l3a, f45), Flt:neg(l3a), true)
            .close();
        outline
            .arcFltTo(cx, cy, l2r, Flt:sub(l2a, f90), Flt:sub(f45n, l2a), false)
            .arcFltTo(cx, cy, l3r, Flt:sub(f45n, l3a), Flt:sub(l3a, f90), true)
            .close();
        outline
            .arcFltTo(cx, cy, l4r, Flt:sub(l4a, f90), Flt:sub(f90, l4a), false)
            .close();
            
        outline
            .arcFltTo(cx, cy, l2r, Flt:add(f90, l2a), Flt:sub(f135, l2a), false)
            .lineFltTo(l5x, l5y)
            .close();
        outline
            .arcFltTo(cx, cy, l2r, Flt:add(f135, l2a), Flt:sub(f225, l2a), false)
            .lineFltTo(Flt:neg(l6x), l6y)
            .lineFltTo(l6x, l6y)
            .close();
        outline
            .arcFltTo(cx, cy, l2r, Flt:add(f225, l2a), Flt:sub(f270, l2a), false)
            .lineFltTo(Flt:neg(l5x), l5y)
            .close();
    }
    
    [] _generateGond(Path gear, Path arrow) {
        {
            int moveTo = true;
            int i = 0;
            int cur = Flt:ZERO_POS;
            while (i++ < 32) {
                [int x1, int y1] := Path:getPolarPoint(g1r, Flt:degToRad(Flt:add(cur, g1a)));
                [int x2, int y2] := Path:getPolarPoint(g2r, Flt:degToRad(Flt:add(cur, g2a)));
                if (moveTo) {
                    gear.moveFltTo(x1, y1);
                    moveTo = false;
                } else {
                    gear.lineFltTo(x1, y1);
                }
                gear.lineFltTo(x2, y2);
                
                cur = Flt:add(cur, gStep);
            }
            gear.close();
        }
        {
            int alpha = 0;
            while (alpha < 360) {
                int cur = Flt:fromInt(alpha);
                int cur2 = Flt:fromInt(alpha + 45);
                [int x9, int y9] := Path:getPolarPoint(g9r, Flt:degToRad(cur2));
                [int x8, int y8] := Path:getPolarPoint(g8r, Flt:degToRad(Flt:add(cur, g8a)));
                [int x8s, int y8s] := Path:getPolarPoint(g8r, Flt:degToRad(Flt:add(cur, g8as)));
                gear.moveFltTo(x9, y9)
                    .arcFltTo(cx, cy, g10r, Flt:sub(cur2, g10a), Flt:add(cur, g10a), true)
                    .lineFltTo(x8, y8)
                    .arcFltTo(cx, cy, g6r, Flt:add(cur, g6a2), Flt:add(cur, g6a2s), true)
                    .lineFltTo(x8s, y8s)
                    .arcFltTo(cx, cy, g10r, Flt:add(cur, g10as), Flt:add(cur2, g10a), true)
                    .close();
                alpha += 90;
            }
        }
        {
            int moveTo = true;
            int alpha = 0;
            while (alpha < 360) {
                int cur = Flt:fromInt(alpha);
                int cur2 = Flt:fromInt(alpha + 45);
                [int x3, int y3] := Path:getPolarPoint(g3r, Flt:degToRad(cur));
                [int x6, int y6] := Path:getPolarPoint(g6r, Flt:degToRad(Flt:add(cur, g6a1)));
                [int x6s, int y6s] := Path:getPolarPoint(g6r, Flt:degToRad(Flt:add(cur, g6a1s)));
                [int x10, int y10] := Path:getPolarPoint(g10r, Flt:degToRad(cur2));
                [int x7, int y7] := Path:getPolarPoint(g7r, Flt:degToRad(Flt:add(cur, g7a)));
                [int x7s, int y7s] := Path:getPolarPoint(g7r, Flt:degToRad(Flt:add(cur, g7as)));
                if (moveTo) {
                    arrow.moveFltTo(x3, y3);
                    moveTo = false;
                } else {
                    arrow.lineFltTo(x3, y3);
                }
                arrow
                    .lineFltTo(x6, y6)
                    .lineFltTo(x7, y7)
                    .arcFltTo(cx, cy, g11r, Flt:add(cur, g11a), Flt:sub(cur2, g11a), true)
                    .lineFltTo(x10, y10)
                    .arcFltTo(cx, cy, g11r, Flt:add(cur2, g11a), Flt:add(cur, g11as), true)
                    .lineFltTo(x7s, y7s)
                    .lineFltTo(x6s, y6s)
                ;
                alpha += 90;
            }
            arrow.close();
            arrow.circleFltTo(cx, cy, g12r, false).close();
        }
        {
            int alpha = 0;
            while (alpha < 360) {
                int cur = Flt:fromInt(alpha);
                arrow
                    .arcFltTo(cx, cy, g4r, Flt:add(cur, g4a), Flt:add(cur, g4as), false)
                    .arcFltTo(cx, cy, g5r, Flt:add(cur, g5as), Flt:add(cur, g5a), true)
                    .close()
                ;
                alpha += 90;
            }
        }
        {
            int moveTo = true;
            int i = 0;
            int cur = Flt:ZERO_POS;
            while (i++ < 32) {
                [int x1, int y1] := Path:getPolarPoint(g1r, Flt:degToRad(Flt:add(cur, g1a)));
                [int x2, int y2] := Path:getPolarPoint(g2r, Flt:degToRad(Flt:add(cur, g2a)));
                if (moveTo) {
                    arrow.moveFltTo(x1, y1);
                    moveTo = false;
                } else {
                    arrow.lineFltTo(x1, y1);
                }
                arrow.lineFltTo(x2, y2);
                
                cur = Flt:add(cur, gStep);
            }
            arrow.close();
        }
        {
            int moveTo = true;
            int i = 0;
            int cur = Flt:ZERO_POS;
            while (i++ < 32) {
                [int x1, int y1] := Path:getPolarPoint(Flt:sub(g1r,Flt:fromInt(8)), Flt:degToRad(Flt:add(cur, g1a)));
                [int x2, int y2] := Path:getPolarPoint(Flt:sub(g2r,Flt:fromInt(4)), Flt:degToRad(Flt:add(cur, g2a)));
                if (moveTo) {
                    arrow.moveFltTo(x1, y1);
                    moveTo = false;
                } else {
                    arrow.lineFltTo(x1, y1);
                }
                arrow.lineFltTo(x2, y2);
                
                cur = Flt:add(cur, gStep);
            }
            arrow.close();
        }
    }
    
    [] _addScaffolding(Graphic g) {
        String c1 := scaffoldingColor1;
        String c2 := scaffoldingColor2;
        String c3 := scaffoldingColor3;
        
        g.addFltSquare(Flt:sub(shiftX, viewWidth), Flt:sub(shiftY, viewWidth), Flt:add(viewWidth, viewWidth))
            .getStrokeStyle().setColor(c1);
            
        int f2 = Flt:fromInt(2);
        
        if (mystraColor) {
            // mystra
            g.addFltCircle(cx, cy, m1r).getStrokeStyle().setColor(c2);
            g.addFltCircle(cx, cy, m2r).getStrokeStyle().setColor(c2);
            g.addFltCircle(cx, cy, m3r).getStrokeStyle().setColor(c2);
            g.addFltCircle(cx, cy, m4r).getStrokeStyle().setColor(c2);
            
            int m1rn = Flt:neg(m1r);
            g.addFltLine(cx,m1r,cx,m1rn).getStrokeStyle().setColor(c2);
            g.addFltLine(m1r,cy,m1rn,cy).getStrokeStyle().setColor(c2);
            g.addFltLine(m1r,m1r,m1rn,m1rn).getStrokeStyle().setColor(c2);
            g.addFltLine(m1r,m1rn,m1rn,m1r).getStrokeStyle().setColor(c2);
            
            [int x, int y] := Path:getPolarPoint(m4r, Flt:degToRad(m4a));
            g.addFltLine(cx, m1r, x, y).getStrokeStyle().setColor(c2);
            g.addFltLine(cx, m1r, Flt:neg(x), y).getStrokeStyle().setColor(c2);
        }
        
        if (lathandarColor) {
            // lathandar
            g.addFltCircle(cx, cy, l1r).getStrokeStyle().setColor(c3);
            g.addFltCircle(cx, cy, l2r).getStrokeStyle().setColor(c3);
            g.addFltCircle(cx, cy, l3r).getStrokeStyle().setColor(c3);
            g.addFltCircle(cx, cy, l4r).getStrokeStyle().setColor(c3);
            
            int w = Flt:div(lWidth, f2);
            int wn = Flt:neg(w);
            int w2 = Flt:mul(w, Flt:SQRT_TWO_POS);
            int l1rn = Flt:neg(l1r);
            
            g.addFltLine(w, l1r, w, l1rn).getStrokeStyle().setColor(c3);
            g.addFltLine(wn, l1r, wn, l1rn).getStrokeStyle().setColor(c3);
            g.addFltLine(l1r, w, l1rn, w).getStrokeStyle().setColor(c3);
            g.addFltLine(l1r, wn, l1rn, wn).getStrokeStyle().setColor(c3);
            
            g.addFltLine(Flt:sub(l1r, w2), l1r, l1rn, Flt:add(l1rn, w2)).getStrokeStyle().setColor(c3);
            g.addFltLine(l1r, Flt:sub(l1r, w2), Flt:add(l1rn, w2), l1rn).getStrokeStyle().setColor(c3);
            g.addFltLine(Flt:add(l1rn, w2), l1r, l1r, Flt:add(l1rn, w2)).getStrokeStyle().setColor(c3);
            g.addFltLine(l1rn, Flt:sub(l1r, w2), Flt:sub(l1r, w2), l1rn).getStrokeStyle().setColor(c3);
            
        }
        
        if (gondColor) {
            // gond
            g.addFltCircle(cx, cy, g1r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g2r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g3r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g4r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g5r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g6r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g7r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g8r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g9r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g10r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g11r).getStrokeStyle().setColor(c1);
            g.addFltCircle(cx, cy, g12r).getStrokeStyle().setColor(c1);
            
            {    
                [int x, int y] := Path:getPolarPoint(g1r, Flt:degToRad(Flt:fromInt(135)));
                g.addFltLine(cx, g1r, x, y).getStrokeStyle().setColor(c1);
                g.addFltLine(cx, g1r, Flt:neg(x), y).getStrokeStyle().setColor(c1);
            }
            
            int w = Flt:div(gWidth, f2);
            g.addFltLine(w, g1r, w, Flt:neg(g1r)).getStrokeStyle().setColor(c1);
            g.addFltLine(Flt:neg(w), g1r, Flt:neg(w), Flt:neg(g1r)).getStrokeStyle().setColor(c1);
            g.addFltLine(g1r, w, Flt:neg(g1r), w).getStrokeStyle().setColor(c1);
            g.addFltLine(g1r, Flt:neg(w), Flt:neg(g1r), Flt:neg(w)).getStrokeStyle().setColor(c1);
        }
        
    }
    
    [] _initializeCoordinates() {
        int f0 = Flt:fromInt(0);
        int f2 = Flt:fromInt(2);
        int f20 = Flt:fromInt(25);
        int f45 = Flt:fromInt(45);
        int f90 = Flt:fromInt(90);
        int f135 = Flt:fromInt(135);
        int f180 = Flt:fromInt(180);
        int f360 = Flt:fromInt(360);

        cx = cy = Flt:fromInt(0);
        {
            // mystra
            m1r = Flt:fromInt(230);
            m2r = Flt:fromInt(170);
            m3r = Flt:fromInt(150);
            m4r = Flt:fromInt(90);
            
            m5r = Flt:fromInt(190);
            m6r = Flt:fromInt(162);
            m7r = Flt:fromInt(158);
            m8r = Flt:fromInt(70);
            
            m1a = Flt:fromInt(0);
            m4a = Flt:div(f360,Flt:fromInt(16));
            
            [int x, int y] := Path:getPolarPoint(m4r, Flt:degToRad(m4a));
            m2a = this.__interAngleCircleLine(cx, cy, m2r, cx, m1r, x, y, true);
            m2as = Flt:sub(f45, m2a);
            m3a = this.__interAngleCircleLine(cx, cy, m3r, cx, m1r, x, y, true);
            m3as = Flt:sub(f45, m3a);
            
            m6a = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, m6r, m1r, f2, Flt:neg(m1r), f2, false));
            m6as = Flt:sub(f45, m6a);
            m7a = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, m7r, m1r, f2, Flt:neg(m1r), f2, false));
            m7as = Flt:sub(f45, m7a);
        }
        {
            // lathandar
            lWidth = Flt:fromInt(8);
            l1r = Flt:fromInt(150);
            l2r = Flt:sub(l1r, lWidth);
            l3r = Flt:div(Flt:add(l1r, lWidth), f2);
            l4r = Flt:sub(l3r, lWidth);
            
            int w = Flt:div(lWidth, f2);
            l2a = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, l2r, l1r, w, Flt:neg(l1r), w, false));
            l3a = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, l3r, l1r, w, Flt:neg(l1r), w, false));
            l4a = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, l4r, l1r, w, Flt:neg(l1r), w, false));
            l6x = Flt:div(l1r, Flt:fromInt(6));
            //l6y = Flt:neg(Flt:mul(w, Flt:SQRT_TWO_POS));
            l6y = Flt:neg(w);
            l5x = Flt:add(l6x, Flt:add(w, Flt:mul(w, Flt:SQRT_TWO_POS)));
            l5y = Flt:neg(w);
        }
        {
            // gond
            int arrow2 := Flt:fromInt(2);
            int arrow := Flt:add(arrow2, arrow2);
            int border := Flt:mul(arrow2, Flt:fromInt(3));
            int width2 := Flt:add(arrow2, border);
            int ab := Flt:add(arrow, border);
            
            gWidth = Flt:add(width2, width2);
            gStep = Flt:div(f360, Flt:fromInt(32));
            g1r = Flt:fromInt(165);
            g2r = Flt:fromInt(150);
            g6r = Flt:fromInt(120);
            g12r = Flt:fromInt(20);
            
            g5r = Flt:add(g6r, border);
            g4r = Flt:add(g5r, arrow);
            g3r = Flt:add(g4r, Flt:add(border, border));
            g7r = Flt:sub(g6r, Flt:add(width2, border));
            g8r = Flt:sub(g7r, ab);
            
            g11r = Flt:add(g12r, border);
            g10r = Flt:add(g11r, ab);
            g9r = Flt:add(g10r, Flt:add(ab, ab));
            
            g1a = f0;
            g2a = Flt:div(gStep, f2);
            {
                g6a1 = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, g6r, g1r, width2, Flt:neg(g1r), width2, false));
                g6a1s = Flt:sub(f90, g6a1);
                g8a = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, g8r, g1r, width2, Flt:neg(g1r), width2, false));
                g8as = Flt:sub(f90, g8a);
                g10a = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, g10r, g1r, width2, Flt:neg(g1r), width2, false));
                g10as = Flt:sub(f90, g10a);
            }
            {
                [int x, int y] := Path:getPolarPoint(g1r, Flt:degToRad(f135));
                g4a = this.__interAngleCircleLine(cx, cy, g4r, cx, g1r, x, y, true);
                g4as = Flt:sub(f90, g4a);
                g5a = this.__interAngleCircleLine(cx, cy, g5r, cx, g1r, x, y, true);
                g5as = Flt:sub(f90, g5a);
                g6a2 = this.__interAngleCircleLine(cx, cy, g6r, cx, g1r, x, y, true);
                g6a2s = Flt:sub(f90, g6a2);
            }
            {
                g7a = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, g7r, g1r, arrow2, Flt:neg(g1r), arrow2, false));
                g7as = Flt:sub(f90, g7a);
                g11a = Flt:sub(f90, this.__interAngleCircleLine(cx, cy, g11r, g1r, arrow2, Flt:neg(g1r), arrow2, false));
                g11as = Flt:sub(f90, g11a);
            }
        }
    }

}
